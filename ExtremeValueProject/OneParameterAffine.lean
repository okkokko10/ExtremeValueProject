/-
Copyright (c) 2025 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§, ...
-/
import Mathlib
import ExtremeValueProject.AffineTransformation

section cauchy_hamel_functional_equation

open Real Set Pointwise MeasureTheory

lemma exists_Ioo_subset_diff_self_of_measure_pos {A : Set ‚Ñù}
    (A_mble : MeasurableSet A) (A_pos : 0 < volume A) :
    ‚àÉ Œ¥ > 0, Ioo (-Œ¥) Œ¥ ‚äÜ A - A := by
  sorry

lemma exists_Ioo_subset_diff_of_measure_pos {A B : Set ‚Ñù}
    (A_mble : MeasurableSet A) (A_pos : 0 < volume A)
    (B_mble : MeasurableSet B) (B_pos : 0 < volume B) :
    ‚àÉ (a b : ‚Ñù), a < b ‚àß Ioo a b ‚äÜ A - B := by
  sorry

lemma exists_Ioo_subset_add_of_measure_pos {A : Set ‚Ñù}
    (A_mble : MeasurableSet A) (A_pos : 0 < volume A) :
    ‚àÉ (a b : ‚Ñù), a < b ‚àß Ioo a b ‚äÜ A + A := by
  obtain ‚ü®a, b, a_lt_b, hab‚ü© := exists_Ioo_subset_diff_of_measure_pos
        A_mble A_pos A_mble.neg (by simpa only [Measure.measure_neg] using A_pos)
  refine ‚ü®a, b, a_lt_b, by simpa only [sub_neg_eq_add] using hab‚ü©

lemma eq_top_of_subgroup_of_measure_pos {S : AddSubgroup ‚Ñù}
    {A : Set ‚Ñù} (A_le_S : A ‚äÜ S) (A_mble : MeasurableSet A) (A_pos : 0 < volume A) :
    S = ‚ä§ := by
  sorry

lemma exists_forall_abs_le_of_additive_of_le_on_measure_pos
    {f : ‚Ñù ‚Üí ‚Ñù} (f_add : ‚àÄ t‚ÇÅ t‚ÇÇ, f (t‚ÇÅ + t‚ÇÇ) = f t‚ÇÅ + f t‚ÇÇ)
    {A : Set ‚Ñù} (A_mble : MeasurableSet A) (A_pos : 0 < volume A)
    {M : ‚Ñù} (f_bdd_on_A : ‚àÄ a ‚àà A, f a ‚â§ M) :
    ‚àÉ Œ¥ > 0, ‚àÉ c, ‚àÄ x ‚àà Ioo (-Œ¥) Œ¥, |f x| ‚â§ c := by
  sorry

open Topology in
lemma exists_nhd_abs_le_of_additive_of_le_on_measure_pos
    {f : ‚Ñù ‚Üí ‚Ñù} (f_add : ‚àÄ t‚ÇÅ t‚ÇÇ, f (t‚ÇÅ + t‚ÇÇ) = f t‚ÇÅ + f t‚ÇÇ)
    {A : Set ‚Ñù} (A_mble : MeasurableSet A) (A_pos : 0 < volume A)
    {M : ‚Ñù} (f_bdd_on_A : ‚àÄ a ‚àà A, f a ‚â§ M) :
    ‚àÉ B ‚àà ùìù (0 : ‚Ñù), ‚àÉ c, ‚àÄ x ‚àà B, |f x| ‚â§ c := by
  obtain ‚ü®Œ¥, Œ¥_pos, hŒ¥‚ü© :=
    exists_forall_abs_le_of_additive_of_le_on_measure_pos f_add A_mble A_pos f_bdd_on_A
  exact ‚ü®Ioo (-Œ¥) Œ¥, Ioo_mem_nhds (by linarith) Œ¥_pos, hŒ¥‚ü©

lemma linear_of_additive_of_le_on_measure_pos
    {f : ‚Ñù ‚Üí ‚Ñù} (f_add : ‚àÄ t‚ÇÅ t‚ÇÇ, f (t‚ÇÅ + t‚ÇÇ) = f t‚ÇÅ + f t‚ÇÇ)
    {A : Set ‚Ñù} (A_mble : MeasurableSet A) (A_pos : 0 < volume A)
    {M : ‚Ñù} (f_bdd_on_A : ‚àÄ a ‚àà A, f a ‚â§ M) (x : ‚Ñù) :
    f x = (f 1) * x := by
  sorry

open ENNReal in
lemma linear_of_additive_of_measurable
    {f : ‚Ñù ‚Üí ‚Ñù} (f_add : ‚àÄ t‚ÇÅ t‚ÇÇ, f (t‚ÇÅ + t‚ÇÇ) = f t‚ÇÅ + f t‚ÇÇ) (f_mble : Measurable f) (x : ‚Ñù) :
    f x = (f 1) * x := by
  set As : ‚Ñï ‚Üí Set ‚Ñù := fun n ‚Ü¶ {y | f y ‚â§ n} with def_As
  have cover : ‚ãÉ n, As n = ‚ä§ := by
    ext x
    simp [exists_nat_ge (f x), def_As]
  have As_mble (n : ‚Ñï) : MeasurableSet (As n) := f_mble measurableSet_Iic
  obtain ‚ü®n, hn‚ü© : ‚àÉ n, 0 < volume (As n) := by
    apply exists_measure_pos_of_not_measure_iUnion_null
    simp [cover]
  exact linear_of_additive_of_le_on_measure_pos f_add (As_mble n) hn (M := n) (by simp [def_As]) x

/-- A measurable additive map ‚Ñù ‚Üí ‚Ñù is linear.
(The only measurable solutions to the Cauchy-Hamel functional equation are the obvious ones.) -/
lemma eq_const_mul_of_additive_of_measurable {f : ‚Ñù ‚Üí ‚Ñù}
    (f_add : ‚àÄ s‚ÇÅ s‚ÇÇ, f (s‚ÇÅ + s‚ÇÇ) = f s‚ÇÅ + f s‚ÇÇ) (f_mble : Measurable f) :
    ‚àÉ Œ±, f = fun s ‚Ü¶ Œ± * s := by
  use f 1
  ext x
  exact linear_of_additive_of_measurable f_add f_mble x

/-- A measurable multiplicative map ‚Ñù ‚Üí (0,+‚àû) is of the form s ‚Ü¶ exp(Œ± * s) for some Œ± ‚àà ‚Ñù.
(The only measurable solutions to the multiplicative version of the Cauchy-Hamel functional
equation are the obvious ones.) -/
lemma eq_exp_const_mul_of_multiplicative_of_measurable {f : ‚Ñù ‚Üí ‚Ñù} (f_pos : ‚àÄ s, 0 < f s)
    (f_multiplicative : ‚àÄ s‚ÇÅ s‚ÇÇ, f (s‚ÇÅ + s‚ÇÇ) = f s‚ÇÅ * f s‚ÇÇ) (f_mble : Measurable f) :
    ‚àÉ Œ±, f = fun s ‚Ü¶ exp (Œ± * s) := by
  let g := fun s ‚Ü¶ log (f s)
  have f_eq_exp_g (s) : f s = exp (g s) := by
    simpa [g] using (exp_log (f_pos s)).symm
  have g_mble : Measurable g := measurable_log.comp f_mble
  have g_additive (s‚ÇÅ s‚ÇÇ) : g (s‚ÇÅ + s‚ÇÇ) = g s‚ÇÅ + g s‚ÇÇ := by
    simpa only [g, f_multiplicative] using log_mul (f_pos _).ne.symm (f_pos _).ne.symm
  obtain ‚ü®Œ±, key‚ü© := eq_const_mul_of_additive_of_measurable g_additive g_mble
  refine ‚ü®Œ±, by ext s ; rw [f_eq_exp_g, key]‚ü©

end cauchy_hamel_functional_equation


section one_parameter_subgroups_of_affine_transformations

/-- The homomorphism `‚Ñù ‚Üí AffineIncrEquiv` given by `s ‚Ü¶ A‚Çõ`, where `A‚Çõ x = x + Œ≤ * s`.
(`Œ≤` is a real parameter: each `Œ≤` gives a different (but related) homomorphism) -/
noncomputable def AffineIncrEquiv.homOfIndex‚ÇÄ (Œ≤ : ‚Ñù) :
    MonoidHom (Multiplicative ‚Ñù) AffineIncrEquiv where
  toFun s := .mkOfCoefs zero_lt_one (s.toAdd * Œ≤)
  map_one' := by ext x ; simp
  map_mul' s‚ÇÅ s‚ÇÇ := by
    ext x
    simp
    ring

/-- The homomorphism `‚Ñù ‚Üí AffineIncrEquiv` given by `s ‚Ü¶ A‚Çõ`, where
`A‚Çõ x = exp(Œ± * s) * (x - c) + c`.
(`Œ± c` are real parameters: each `Œ± c` give a different homomorphism) -/
noncomputable def AffineIncrEquiv.homOfIndex (Œ± c : ‚Ñù) :
    MonoidHom (Multiplicative ‚Ñù) AffineIncrEquiv where
  toFun s := .mkOfCoefs (show 0 < Real.exp (s.toAdd * Œ±) from Real.exp_pos _)
              (c * (1 - Real.exp (s.toAdd * Œ±)))
  map_one' := by ext x ; simp
  map_mul' s‚ÇÅ s‚ÇÇ := by
    ext x
    simp [add_mul, Real.exp_add]
    ring

@[simp] lemma AffineIncrEquiv.homOfIndex‚ÇÄ_coefs_fst {Œ≤ s : ‚Ñù} :
    (homOfIndex‚ÇÄ Œ≤ s).coefs.1 = 1 := by
  simp [homOfIndex‚ÇÄ, MonoidHom.coe_mk, OneHom.coe_mk, coefs_fst_mkOfCoefs]

@[simp] lemma AffineIncrEquiv.homOfIndex‚ÇÄ_coefs_snd {Œ≤ s : ‚Ñù} :
    (homOfIndex‚ÇÄ Œ≤ s).coefs.2 = s * Œ≤ := by
  simp only [homOfIndex‚ÇÄ, MonoidHom.coe_mk, OneHom.coe_mk, coefs_snd_mkOfCoefs]
  congr

@[simp] lemma AffineIncrEquiv.homOfIndex_coefs_fst {Œ± c s : ‚Ñù} :
    (homOfIndex Œ± c s).coefs.1 = Real.exp (s * Œ±) := by
  simp only [homOfIndex, MonoidHom.coe_mk, OneHom.coe_mk, coefs_fst_mkOfCoefs, Real.exp_eq_exp]
  congr

@[simp] lemma AffineIncrEquiv.homOfIndex_coefs_snd {Œ± c s : ‚Ñù} :
    (homOfIndex Œ± c s).coefs.2 = c * (1 - Real.exp (s * Œ±)) := by
  simp only [homOfIndex, MonoidHom.coe_mk, OneHom.coe_mk, coefs_snd_mkOfCoefs]
  congr

@[simp] lemma AffineIncrEquiv.homOfIndex‚ÇÄ_zero' (Œ≤ : ‚Ñù) :
    homOfIndex‚ÇÄ Œ≤ (.ofAdd 0) = 1 :=
  map_one ..

@[simp] lemma AffineIncrEquiv.homOfIndex‚ÇÄ_zero (Œ≤ : ‚Ñù) :
    homOfIndex‚ÇÄ Œ≤ (@OfNat.ofNat ‚Ñù 0 Zero.toOfNat0) = 1 :=
  map_one ..

lemma AffineIncrEquiv.homOfIndex‚ÇÄ_zero_apply' (Œ≤ : ‚Ñù) (x : ‚Ñù) :
    homOfIndex‚ÇÄ Œ≤ (.ofAdd 0) x = x := by
  simp

lemma AffineIncrEquiv.homOfIndex‚ÇÄ_zero_apply (Œ≤ : ‚Ñù) (x : ‚Ñù) :
    homOfIndex‚ÇÄ Œ≤ (@OfNat.ofNat ‚Ñù 0 Zero.toOfNat0) x = x := by
  simp

lemma AffineIncrEquiv.homOfIndex‚ÇÄ_add (Œ≤ : ‚Ñù) (s‚ÇÅ s‚ÇÇ : ‚Ñù) :
    homOfIndex‚ÇÄ Œ≤ (s‚ÇÅ + s‚ÇÇ) = homOfIndex‚ÇÄ Œ≤ s‚ÇÅ * homOfIndex‚ÇÄ Œ≤ s‚ÇÇ :=
  map_mul ..

@[simp] lemma AffineIncrEquiv.homOfIndex‚ÇÄ_inv (Œ≤ : ‚Ñù) (s : ‚Ñù) :
    (homOfIndex‚ÇÄ Œ≤ s)‚Åª¬π = homOfIndex‚ÇÄ Œ≤ (-s) := by
  have obs := homOfIndex‚ÇÄ_add Œ≤ s (-s)
  simp only [add_neg_cancel, homOfIndex‚ÇÄ_zero] at obs
  exact DivisionMonoid.inv_eq_of_mul _ _ obs.symm

@[simp] lemma AffineIncrEquiv.homOfIndex‚ÇÄ_add_apply {Œ≤ : ‚Ñù} {s‚ÇÅ s‚ÇÇ : ‚Ñù} (x : ‚Ñù) :
    homOfIndex‚ÇÄ Œ≤ (s‚ÇÅ + s‚ÇÇ) x = homOfIndex‚ÇÄ Œ≤ s‚ÇÅ (homOfIndex‚ÇÄ Œ≤ s‚ÇÇ x) := by
  simp only [homOfIndex‚ÇÄ_add, mul_apply_eq_comp_apply]

lemma AffineIncrEquiv.homOfIndex‚ÇÄ_eq_homOfIndex‚ÇÄ_one_mul {Œ≤ s : ‚Ñù} :
    homOfIndex‚ÇÄ Œ≤ s = homOfIndex‚ÇÄ 1 (Œ≤ * s) := by
  ext x
  simp [mul_comm]

lemma AffineIncrEquiv.conjugate_homOfIndex‚ÇÄ (A : AffineIncrEquiv) (Œ≤ : ‚Ñù) (s : ‚Ñù) :
    A * homOfIndex‚ÇÄ Œ≤ s * A‚Åª¬π = homOfIndex‚ÇÄ (Œ≤ * A.coefs.1) s := by
  sorry -- **Issue #46**

@[simp] lemma AffineIncrEquiv.homOfIndex_zero' (Œ± c : ‚Ñù) :
    homOfIndex Œ± c (.ofAdd 0) = 1 :=
  map_one ..

@[simp] lemma AffineIncrEquiv.homOfIndex_zero (Œ± c : ‚Ñù) :
    homOfIndex Œ± c (@OfNat.ofNat ‚Ñù 0 Zero.toOfNat0) = 1 :=
  map_one ..

lemma AffineIncrEquiv.homOfIndex_zero_apply' (Œ± c : ‚Ñù) (x : ‚Ñù) :
    homOfIndex Œ± c (.ofAdd 0) x = x := by
  simp

lemma AffineIncrEquiv.homOfIndex_zero_apply (Œ± c : ‚Ñù) (x : ‚Ñù) :
    homOfIndex Œ± c (@OfNat.ofNat ‚Ñù 0 Zero.toOfNat0) x = x := by
  simp

lemma AffineIncrEquiv.homOfIndex_add (Œ± c : ‚Ñù) (s‚ÇÅ s‚ÇÇ : ‚Ñù) :
    homOfIndex Œ± c (s‚ÇÅ + s‚ÇÇ) = homOfIndex Œ± c s‚ÇÅ * homOfIndex Œ± c s‚ÇÇ :=
  map_mul ..

@[simp] lemma AffineIncrEquiv.homOfIndex_inv (Œ± c : ‚Ñù) (s : ‚Ñù) :
    (homOfIndex Œ± c s)‚Åª¬π = homOfIndex Œ± c (-s) := by
  have obs := homOfIndex_add Œ± c s (-s)
  simp only [add_neg_cancel, homOfIndex_zero] at obs
  exact DivisionMonoid.inv_eq_of_mul _ _ obs.symm

@[simp] lemma AffineIncrEquiv.homOfIndex_add_apply {Œ± c : ‚Ñù} {s‚ÇÅ s‚ÇÇ : ‚Ñù} (x : ‚Ñù) :
    homOfIndex Œ± c (s‚ÇÅ + s‚ÇÇ) x = homOfIndex Œ± c s‚ÇÅ (homOfIndex Œ± c s‚ÇÇ x) := by
  simp only [homOfIndex_add, mul_apply_eq_comp_apply]

lemma AffineIncrEquiv.homOfIndex_eq_homOfIndex_one_mul {Œ± c s : ‚Ñù} :
    homOfIndex Œ± c s = homOfIndex 1 c (s * Œ±) := by
  ext x
  simp

lemma AffineIncrEquiv.conjugate_homOfIndex (A : AffineIncrEquiv) (Œ± c : ‚Ñù) (s : ‚Ñù) :
    A * homOfIndex Œ± c s * A‚Åª¬π = homOfIndex Œ± (A c) s := by
  sorry -- **Issue #46**

/-- The one-parameter subgroup of `AffineIncrEquiv` consisting of elements `A‚Çõ` of the form
`A‚Çõ x = x + Œ≤ * s`, where `s ‚àà ‚Ñù`.
(`Œ≤` is a real parameter: each `Œ≤ ‚â† 0` in fact gives the same subgroup) -/
noncomputable def AffineIncrEquiv.subGroupOfIndex‚ÇÄ' (Œ≤ : ‚Ñù) :
    Subgroup AffineIncrEquiv :=
  Subgroup.map (AffineIncrEquiv.homOfIndex‚ÇÄ Œ≤) ‚ä§

/-- The one-parameter subgroup of `AffineIncrEquiv` consisting of elements `A‚Çõ` of the form
`A‚Çõ x = x + s`, where `s ‚àà ‚Ñù`. -/
noncomputable def AffineIncrEquiv.subGroupOfIndex‚ÇÄ :
    Subgroup AffineIncrEquiv :=
  Subgroup.map (AffineIncrEquiv.homOfIndex‚ÇÄ 1) ‚ä§

@[simp] lemma AffineIncrEquiv.subGroupOfIndex‚ÇÄ'_eq_of_ne_zero {Œ≤ : ‚Ñù} (hŒ≤ : Œ≤ ‚â† 0) :
    AffineIncrEquiv.subGroupOfIndex‚ÇÄ' Œ≤ = AffineIncrEquiv.subGroupOfIndex‚ÇÄ := by
  sorry -- **Issue 44**

@[simp] lemma AffineIncrEquiv.subGroupOfIndex‚ÇÄ'_eq_bot :
    AffineIncrEquiv.subGroupOfIndex‚ÇÄ' 0 = ‚ä• := by
  sorry -- **Issue 44**

@[simp] lemma AffineIncrEquiv.mem_subGroupOfIndex‚ÇÄ_of_no_fixed_point (A : AffineIncrEquiv)
    {Œ± : ‚Ñù} (hŒ± : Œ± ‚â† 0) (c : ‚Ñù) (hA : ‚àÄ x, A x ‚â† x) :
    A ‚àà subGroupOfIndex‚ÇÄ := by
  sorry -- **Issue 44**

/-- The one-parameter subgroup of `AffineIncrEquiv` consisting of elements `A‚Çõ` of the form
`A‚Çõ x = exp(Œ± * s) * (x - c) + c` where `s ‚àà ‚Ñù`.
(`Œ± c` are real parameters) -/
noncomputable def AffineIncrEquiv.subGroupOfIndex (Œ± c : ‚Ñù) :
    Subgroup AffineIncrEquiv :=
  Subgroup.map (AffineIncrEquiv.homOfIndex Œ± c) ‚ä§

@[simp] lemma AffineIncrEquiv.subGroupOfIndex_eq_bot (c : ‚Ñù) :
    subGroupOfIndex 0 c = ‚ä• := by
  sorry -- **Issue 45**

@[simp] lemma AffineIncrEquiv.fixed_point_of_mem_subGroupOfIndex (A : AffineIncrEquiv)
    {Œ± c : ‚Ñù} (hA : A ‚àà subGroupOfIndex Œ± c):
    A c = c := by
  obtain ‚ü®s, _, hs‚ü© := hA
  simp only [‚Üê hs, apply_eq, homOfIndex_coefs_fst, homOfIndex_coefs_snd]
  ring

@[simp] lemma AffineIncrEquiv.mem_subGroupOfIndex_iff_fixed_point (A : AffineIncrEquiv)
    {Œ± : ‚Ñù} (hŒ± : Œ± ‚â† 0) (c : ‚Ñù) :
    A ‚àà subGroupOfIndex Œ± c ‚Üî A c = c := by
  sorry -- **Issue 45**

/-- Functional equation for scaling coefficients of a homomorphism `f : ‚Ñù ‚Üí AffineIncrEquiv`. -/
lemma AffineIncrEquiv.homomorphism_coef_eqn_fst
    (f : MonoidHom (Multiplicative ‚Ñù) AffineIncrEquiv) (s‚ÇÅ s‚ÇÇ : ‚Ñù) :
    (f (s‚ÇÅ + s‚ÇÇ)).coefs.1 = (f s‚ÇÅ).coefs.1 * (f s‚ÇÇ).coefs.1 := by
  simp [show f (s‚ÇÅ + s‚ÇÇ) = f s‚ÇÅ * f s‚ÇÇ by rw [‚Üê f.map_mul] ; rfl]

/-- Functional equation for translation coefficients of a homomorphism `f : ‚Ñù ‚Üí AffineIncrEquiv`. -/
lemma AffineIncrEquiv.homomorphism_coef_eqn_snd
    (f : MonoidHom (Multiplicative ‚Ñù) AffineIncrEquiv) (s‚ÇÅ s‚ÇÇ : ‚Ñù) :
    (f (s‚ÇÅ + s‚ÇÇ)).coefs.2 = (f s‚ÇÅ).coefs.1 * (f s‚ÇÇ).coefs.2 + (f s‚ÇÅ).coefs.2 := by
  simp [show f (s‚ÇÅ + s‚ÇÇ) = f s‚ÇÅ * f s‚ÇÇ by rw [‚Üê f.map_mul] ; rfl]

open Real

lemma eq_of_functional_eqn_of_ne_zero {f : ‚Ñù ‚Üí ‚Ñù} {Œ± : ‚Ñù} (Œ±_ne_zero : Œ± ‚â† 0)
    (f_eqn : ‚àÄ s‚ÇÅ s‚ÇÇ, f (s‚ÇÅ + s‚ÇÇ) = exp (Œ± * s‚ÇÅ) * f s‚ÇÇ + f s‚ÇÅ) :
    ‚àÉ c, f = fun s ‚Ü¶ c * (1 - exp (Œ± * s)) := by
  sorry

/-- We endow the space of orientation-preserving affine isomorphisms of `‚Ñù` with the Borel
œÉ-algebra of the topology of pointwise convergence. -/
instance : MeasurableSpace AffineIncrEquiv := borel AffineIncrEquiv

instance : BorelSpace AffineIncrEquiv := ‚ü®rfl‚ü©

lemma AffineIncrEquiv.measurable_coefs_fst :
    Measurable (fun (A : AffineIncrEquiv) ‚Ü¶ A.coefs.1) :=
  continuous_coefs_fst.measurable

lemma AffineIncrEquiv.measurable_coefs_snd :
    Measurable (fun (A : AffineIncrEquiv) ‚Ü¶ A.coefs.2) :=
  continuous_coefs_snd.measurable

lemma AffineIncrEquiv.continuous_mkOfCoefs :
    Continuous fun (p : {a : ‚Ñù // 0 < a} √ó ‚Ñù) ‚Ü¶ mkOfCoefs p.1.prop p.2 := by
  apply (continuous_induced_rng ..).mpr
  exact continuous_pi (by continuity)

lemma AffineIncrEquiv.measurable_mkOfCoefs :
    Measurable fun (p : {a : ‚Ñù // 0 < a} √ó ‚Ñù) ‚Ü¶ mkOfCoefs p.1.prop p.2 := by
  have _bs1 : BorelSpace {a : ‚Ñù // 0 < a} := Subtype.borelSpace _
  have _bs2 : BorelSpace ({a : ‚Ñù // 0 < a} √ó ‚Ñù) := Prod.borelSpace
  exact continuous_mkOfCoefs.measurable

lemma AffineIncrEquiv.continuous_of_continuous_coefs {Z : Type*} [TopologicalSpace Z]
    {f : Z ‚Üí AffineIncrEquiv} (f_fst_cont : Continuous fun z ‚Ü¶ (f z).coefs.1)
    (f_snd_cont : Continuous fun z ‚Ü¶ (f z).coefs.2) :
    Continuous f := by
  convert AffineIncrEquiv.continuous_mkOfCoefs.comp <|
    show Continuous fun z ‚Ü¶ (‚ü®‚ü®(f z).coefs.1, (f z).isOrientationPreserving‚ü©, (f z).coefs.2‚ü©) by
      continuity
  ext z x
  simp

lemma AffineIncrEquiv.measurable_of_measurable_coefs {Z : Type*} [MeasurableSpace Z]
    {f : Z ‚Üí AffineIncrEquiv} (f_fst_cont : Measurable fun z ‚Ü¶ (f z).coefs.1)
    (f_snd_cont : Measurable fun z ‚Ü¶ (f z).coefs.2) :
    Measurable f := by
  convert AffineIncrEquiv.measurable_mkOfCoefs.comp <|
    show Measurable fun z ‚Ü¶ (‚ü®‚ü®(f z).coefs.1, (f z).isOrientationPreserving‚ü©, (f z).coefs.2‚ü©) by
      measurability
  ext z x
  simp

instance : MeasurableSpace (Multiplicative ‚Ñù) := borel (Multiplicative ‚Ñù)

instance : BorelSpace (Multiplicative ‚Ñù) := ‚ü®rfl‚ü©

lemma measurable_toAdd :
    Measurable (fun (s : Multiplicative ‚Ñù) ‚Ü¶ s.toAdd) :=
  continuous_toAdd.measurable

lemma measurable_toMultiplicative :
    Measurable (fun (s : ‚Ñù) ‚Ü¶ Multiplicative.ofAdd s) :=
  continuous_ofAdd.measurable

/-- Characterization of homomorphisms `f : ‚Ñù ‚Üí AffineIncrEquiv`. -/
theorem AffineIncrEquiv.homomorphism_from_Real_characterization
    (f : MonoidHom (Multiplicative ‚Ñù) AffineIncrEquiv) (f_mble : Measurable f) :
    (‚àÉ Œ≤, f = homOfIndex‚ÇÄ Œ≤) ‚à® (‚àÉ Œ± c, f = homOfIndex Œ± c) := by
  sorry -- TODO: Create issue.

/-- Characterization of nontrivial homomorphisms `f : ‚Ñù ‚Üí AffineIncrEquiv`. -/
theorem AffineIncrEquiv.homomorphism_from_Real_characterization_of_nontrivial
    {f : MonoidHom (Multiplicative ‚Ñù) AffineIncrEquiv} (f_nontriv : ¬¨ f = 1)
    (f_mble : Measurable f) :
    (‚àÉ Œ≤, Œ≤ ‚â† 0 ‚àß f = homOfIndex‚ÇÄ Œ≤) ‚à® (‚àÉ Œ± c, Œ± ‚â† 0 ‚àß f = homOfIndex Œ± c) := by
  cases' homomorphism_from_Real_characterization f f_mble with h‚ÇÄ h‚ÇÅ
  ¬∑ obtain ‚ü®Œ≤, hŒ≤‚ü© := h‚ÇÄ
    refine Or.inl ‚ü®Œ≤, ?_, hŒ≤‚ü©
    by_contra maybe_zero
    apply f_nontriv
    ext x
    simp [hŒ≤, maybe_zero]
  ¬∑ obtain ‚ü®Œ±, c, h‚ü© := h‚ÇÅ
    refine Or.inr ‚ü®Œ±, c, ?_, h‚ü©
    by_contra maybe_zero
    apply f_nontriv
    ext x
    simp [h, maybe_zero]

end one_parameter_subgroups_of_affine_transformations
