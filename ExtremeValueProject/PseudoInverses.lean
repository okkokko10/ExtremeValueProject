/-
Copyright (c) 2025 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤, ...
-/
import Mathlib.Order.CompletePartialOrder
import Mathlib.Order.OrdContinuous
import Mathlib.Topology.Order.DenselyOrdered

open Set

noncomputable section order_continuity_vs_continuity
/-!
# Left/right-order-continuous functions are (topologically) left/right-continuous

Below is an extended version of PR #23309 to Mathlib: here we do the pointwise
`ContinuousAt` case with weaker assumptions, and the converse implication as well.
These are auxiliary results for the ExtremeValueProject.
-/

section ConditionallyCompleteLinearOrder

variable {R S : Type} [LinearOrder R] [LinearOrder S] [DenselyOrdered R]

lemma forall_isLUB_imp_isLUB_image_iff_isLUB_image_Iio {f : R â†’ S} (f_mono : Monotone f) {x : R} :
    (âˆ€ (s : Set R), IsLUB s x â†’ IsLUB (f '' s) (f x)) â†” IsLUB (f '' Iio x) (f x) := by
  constructor
  Â· intro f_lcont
    exact f_lcont _ isLUB_Iio
  Â· intro h s hx
    refine âŸ¨by simpa [mem_upperBounds] using fun a ha â†¦ f_mono (hx.1 ha), ?_âŸ©
    intro y hy
    apply h.2
    simp only [mem_lowerBounds, mem_upperBounds, mem_image, forall_exists_index, and_imp,
               forall_apply_eq_imp_iffâ‚‚] at hy âŠ¢
    intro a a_lt
    obtain âŸ¨b, b_in_s, hbâŸ© : âˆƒ b âˆˆ s, a < b := by rwa [mem_Iio, lt_isLUB_iff hx] at a_lt
    by_contra con
    apply lt_irrefl _ <| (not_le.mp con).trans_le <| (f_mono hb.le).trans (hy b b_in_s)

lemma forall_isGLB_imp_isGLB_image_iff_isGLB_image_Ioi {f : R â†’ S} (f_mono : Monotone f) {x : R} :
    (âˆ€ (s : Set R), IsGLB s x â†’ IsGLB (f '' s) (f x)) â†” IsGLB (f '' Ioi x) (f x) :=
  forall_isLUB_imp_isLUB_image_iff_isLUB_image_Iio (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) <|
    by exact fun _ _ hx â†¦ f_mono hx

lemma leftOrdContinuous_iff_forall_isLUB_image_Iio {f : R â†’ S} (f_mono : Monotone f) :
    LeftOrdContinuous f â†” âˆ€ (x : R), IsLUB (f '' Iio x) (f x) := by
  simp_rw [â† forall_isLUB_imp_isLUB_image_iff_isLUB_image_Iio f_mono, LeftOrdContinuous]
  aesop

lemma rightOrdContinuous_iff_forall_isGLB_image_Ioi {f : R â†’ S} (f_mono : Monotone f) :
    RightOrdContinuous f â†” âˆ€ (x : R), IsGLB (f '' Ioi x) (f x) :=
  leftOrdContinuous_iff_forall_isLUB_image_Iio (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) <|
    by exact fun _ _ hx â†¦ f_mono hx

open Topology

lemma Monotone.isLUB_image_Iio_of_continuousWithinAt_Iic' {R S : Type*}
    [PartialOrder R] [TopologicalSpace R] [PartialOrder S] [TopologicalSpace S]
    [ClosedIicTopology S] {f : R â†’ S} (f_mono : Monotone f) {x : R}
    (hx : (ð“[<] x).NeBot) (f_cont : ContinuousWithinAt f (Iic x) x) :
    IsLUB (f '' Iio x) (f x) := by
  rw [â† continuousWithinAt_Iio_iff_Iic] at f_cont
  refine âŸ¨?_, ?_âŸ©
  Â· simpa [mem_upperBounds] using fun a ha â†¦ f_mono ha.le
  Â· simp only [mem_lowerBounds, mem_upperBounds, mem_image, mem_Iio, forall_exists_index,
               and_imp, forall_apply_eq_imp_iffâ‚‚]
    intro y hy
    apply le_of_tendsto_of_frequently f_cont
    apply Filter.frequently_iff.mpr
    intro U hU
    obtain âŸ¨t, htâŸ© := Filter.Eventually.exists (Filter.inter_mem hU self_mem_nhdsWithin)
    exact âŸ¨t, ht.1, hy _ ht.2âŸ©

lemma Monotone.isLUB_image_Iio_of_continuousWithinAt_Iic {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [PartialOrder S] [TopologicalSpace S] [ClosedIicTopology S]
    {f : R â†’ S} (f_mono : Monotone f) {x : R} (hx : Â¬ IsMin x)
    (f_cont : ContinuousWithinAt f (Iic x) x) :
    IsLUB (f '' Iio x) (f x) := by
  apply f_mono.isLUB_image_Iio_of_continuousWithinAt_Iic' _ f_cont
  apply mem_closure_iff_nhdsWithin_neBot.mp
  rw [closure_Iio' (Iio_nonempty.mpr hx)]
  exact right_mem_Iic

lemma Monotone.continuousWithinAt_Iic_of_isLUB_image_Iio {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R]
    [LinearOrder S] [TopologicalSpace S] [OrderTopology S]
    {f : R â†’ S} (f_mono : Monotone f) {x : R} (hf : IsLUB (f '' Iio x) (f x)) :
    ContinuousWithinAt f (Iic x) x := by
  rw [ContinuousWithinAt, OrderTopology.topology_eq_generate_intervals (Î± := S)]
  simp_rw [TopologicalSpace.tendsto_nhds_generateFrom_iff, mem_nhdsWithin]
  rintro V âŸ¨z, rfl | rflâŸ© hxz
  -- The case `V = Ioi z`.
  Â· obtain âŸ¨_, âŸ¨a, hax, rflâŸ©, hzaâŸ© := (lt_isLUB_iff <| hf).mp hxz
    exact âŸ¨Ioi a, isOpen_Ioi, hax, fun b hab â†¦ hza.trans_le <| f_mono hab.1.leâŸ©
  -- The case `V = Iio z`.
  Â· exact âŸ¨univ, isOpen_univ, trivial, fun a ha â†¦ (f_mono ha.2).trans_lt hxzâŸ©

lemma Monotone.continuousWithinAt_Iic_iff_isLUB_image_Iio' {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R]
    [LinearOrder S] [TopologicalSpace S] [OrderTopology S]
    {f : R â†’ S} (f_mono : Monotone f) {x : R} (hx : (ð“[<] x).NeBot) :
    ContinuousWithinAt f (Iic x) x â†” IsLUB (f '' Iio x) (f x) :=
  âŸ¨isLUB_image_Iio_of_continuousWithinAt_Iic' f_mono hx,
   f_mono.continuousWithinAt_Iic_of_isLUB_image_IioâŸ©

lemma Monotone.continuousWithinAt_Iic_iff_isLUB_image_Iio {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [LinearOrder S] [TopologicalSpace S] [OrderTopology S]
    {f : R â†’ S} (f_mono : Monotone f) {x : R} (hx : Â¬ IsMin x) :
    ContinuousWithinAt f (Iic x) x â†” IsLUB (f '' Iio x) (f x) :=
  âŸ¨isLUB_image_Iio_of_continuousWithinAt_Iic f_mono hx,
   f_mono.continuousWithinAt_Iic_of_isLUB_image_IioâŸ©

variable {X : Type*} [ConditionallyCompleteLinearOrder X] [TopologicalSpace X] [OrderTopology X]
variable {Y : Type*} [ConditionallyCompleteLinearOrder Y] [TopologicalSpace Y] [OrderTopology Y]
variable [DenselyOrdered X] {f : X â†’ Y} {x : X}

/-- An order-theoretically left-continuous function is topologically left-continuous, assuming
the function is between conditionally complete linear orders with order topologies, and the domain
is densely ordered. -/
lemma LeftOrdContinuous.continuousWithinAt_Iic (hf : LeftOrdContinuous f) :
    ContinuousWithinAt f (Iic x) x :=
  Monotone.continuousWithinAt_Iic_of_isLUB_image_Iio hf.mono (hf isLUB_Iio)

/-- An order-theoretically right-continuous function is topologically right-continuous, assuming
the function is between conditionally complete linear orders with order topologies, and the domain
is densely ordered. -/
lemma RightOrdContinuous.continuousWithinAt_Ici (hf : RightOrdContinuous f) :
    ContinuousWithinAt f (Ici x) x := hf.orderDual.continuousWithinAt_Iic

-- TODO: Add `ContinuousAt` versions with weaker (pointwise) hypotheses than left/right
-- order continuity?

end ConditionallyCompleteLinearOrder

end order_continuity_vs_continuity


noncomputable section modification
/-!
# Left-continuous and right-continuous modifications of functions
-/

variable {R S : Type*}

/-- Left-continuous modification of F. -/
def lcMod [Preorder R] [SupSet S] (F : R â†’ S) (x : R) := sSup (F '' Iio x)

/-- right-continuous modification of F. -/
def rcMod [Preorder R] [InfSet S] (F : R â†’ S) (x : R) := sInf (F '' Ioi x)

lemma lcMod_mono [Preorder R] [CompleteSemilatticeSup S] (F : R â†’ S) :
    Monotone (lcMod F) :=
  fun _ _ hx â†¦ sSup_le_sSup <| image_mono <| Iio_subset_Iio hx

lemma rcMod_mono [Preorder R] [CompleteSemilatticeInf S] (F : R â†’ S) :
    Monotone (rcMod F) :=
  fun _ _ hx â†¦ sInf_le_sInf <| image_mono <| Ioi_subset_Ioi hx

lemma lcMod_apply_le_self_apply [Preorder R] [CompleteSemilatticeSup S] {F : R â†’ S}
    (F_mono : Monotone F) (x : R) :
    lcMod F x â‰¤ F x := by
  apply sSup_le
  intro y âŸ¨x', x'_lt, Fx'_eq_yâŸ©
  simpa [â† Fx'_eq_y] using F_mono x'_lt.le

lemma self_apply_le_rcMod_apply [Preorder R] [CompleteSemilatticeInf S] {F : R â†’ S}
    (F_mono : Monotone F) (x : R) :
    F x â‰¤ rcMod F x :=
  lcMod_apply_le_self_apply (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) (F := F) (by exact fun _ _ hx â†¦ F_mono hx) x

lemma lcMod_le_self [Preorder R] [CompleteSemilatticeSup S] {F : R â†’ S} (F_mono : Monotone F) :
    lcMod F â‰¤ F :=
  fun x â†¦ lcMod_apply_le_self_apply F_mono x

lemma self_le_rcMod [Preorder R] [CompleteSemilatticeInf S] {F : R â†’ S} (F_mono : Monotone F) :
    F â‰¤ rcMod F :=
  fun x â†¦ self_apply_le_rcMod_apply F_mono x

lemma le_lcMod_apply_of_lt [Preorder R] [CompleteSemilatticeSup S] (F : R â†’ S) {x' x : R}
    (h : x' < x) :
    F x' â‰¤ lcMod F x :=
  le_sSup (mem_image_of_mem F h)

lemma rcMod_apply_le_of_lt [Preorder R] [CompleteSemilatticeInf S] (F : R â†’ S) {x' x : R}
    (h : x < x') :
    rcMod F x â‰¤ F x' :=
  sInf_le (mem_image_of_mem F h)

/-- The left-continuous modification of a function is left-continuous. -/
lemma leftOrdContinuous_lcMod [LinearOrder R] [CompleteSemilatticeSup S] (F : R â†’ S) :
    LeftOrdContinuous (lcMod F) := by
  intro s lub_s h_lub_s
  refine âŸ¨?_, ?_âŸ©
  Â· simp only [mem_upperBounds, mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
    intro x x_in_s
    exact lcMod_mono _ (h_lub_s.1 x_in_s)
  Â· simp only [mem_lowerBounds, mem_upperBounds, mem_image, forall_exists_index, and_imp,
               forall_apply_eq_imp_iffâ‚‚]
    intro y hy
    apply sSup_le
    intro z âŸ¨x, hx, Fx_eq_zâŸ©
    rw [â† Fx_eq_z]
    obtain âŸ¨x', x'_in_s, x_lt_x'âŸ© : âˆƒ x' âˆˆ s, x < x' := by
      by_contra con
      simp only [not_exists, not_and, not_lt] at con
      exact lt_irrefl _ (hx.trans_le (h_lub_s.2 con))
    exact (le_lcMod_apply_of_lt F x_lt_x').trans (hy x' x'_in_s)

/-- The right-continuous modification of a function is right-continuous. -/
lemma rightOrdContinuous_rcMod [LinearOrder R] [CompleteSemilatticeInf S](F : R â†’ S) :
    RightOrdContinuous (rcMod F) :=
  leftOrdContinuous_lcMod (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) (F := F)

end modification


noncomputable section pseudoinverses
/-!
# Left-continuous and right-continuous (pseudo)inverses
-/

variable {R S : Type*} [CompleteLinearOrder R] [CompleteLinearOrder S]

/-- The left-continuous pseudoinverse of a function. -/
def lcInv (F : R â†’ S) (y : S) := sInf {x | y â‰¤ F x}

/-- The right-continuous pseudoinverse of a function. -/
def rcInv (F : R â†’ S) (y : S) := sSup {x | F x â‰¤ y}

lemma rcInv_eq_lcInv_dual (F : R â†’ S) :
    rcInv F = @lcInv Ráµ’áµˆ Sáµ’áµˆ _ _ F :=
  rfl

variable {F : R â†’ S}

lemma lcInv_le_of_apply_ge {x : R} {y : S} (h : y â‰¤ F x) :
    lcInv F y â‰¤ x :=
  sInf_le h

lemma lcInv_ge_of_apply_lt {x : R} {y : S} (F_mono : Monotone F) (h : F x < y) :
    x â‰¤ lcInv F y := by
  apply le_sInf
  intro b hb
  by_contra con
  exact lt_irrefl _ <| (hb.trans (F_mono (not_le.mp con).le)).trans_lt h

lemma forall_apply_ge_of_lcInv_le {x : R} {y : S} (F_mono : Monotone F) (h : lcInv F y â‰¤ x)
    {x' : R} (x_lt_x' : x < x') :
    y â‰¤ F x' := by
  by_contra con
  have obs (z : R) (hz : z âˆˆ {z | y â‰¤ F z}) : x' â‰¤ z := by
    by_contra con'
    exact lt_irrefl _ <| ((not_le.mp con).trans_le hz).trans_le (F_mono (not_le.mp con').le)
  exact lt_irrefl _ <| (h.trans_lt x_lt_x').trans_le (le_sInf obs)

lemma forall_apply_lt_of_lcInv_ge {x : R} {y : S} (h : x â‰¤ lcInv F y) {x' : R} (x'_lt_x : x' < x) :
    F x' < y := by
  by_contra con
  have key := sInf_le (show x' âˆˆ {z | y â‰¤ F z} from not_lt.mp con)
  exact lt_irrefl _ <| (key.trans_lt x'_lt_x).trans_le h

lemma lcInv_lt_iff_exists_apply_ge {x : R} {y : S} :
    lcInv F y < x â†” âˆƒ x' < x, y â‰¤ F x' := by
  rw [lcInv, sInf_lt_iff]
  exact âŸ¨fun âŸ¨a, ha, a_ltâŸ© â†¦ âŸ¨a, a_lt, haâŸ©, fun âŸ¨a, a_lt, haâŸ© â†¦ âŸ¨a, ha, a_ltâŸ©âŸ©

lemma exists_apply_lt_of_lcInv_gt [DenselyOrdered R] {x : R} {y : S} (h : x < lcInv F y) :
    âˆƒ x' > x, F x' < y := by
  obtain âŸ¨w, x_lt_w, w_ltâŸ© := exists_between h
  by_contra con
  simp only [gt_iff_lt, not_exists, not_and, not_lt] at con
  exact lt_irrefl _ <| (lcInv_le_of_apply_ge (con w x_lt_w)).trans_lt w_lt

lemma lcInv_gt_of_exists_apply_lt [DenselyOrdered R] (F_mono : Monotone F) {x : R} {y : S}
    {x' : R} (x_lt_x' : x' > x) (h : F x' < y) :
    x < lcInv F y := by
  obtain âŸ¨w, x_lt_w, w_lt_x'âŸ© := exists_between x_lt_x'
  by_contra con
  have bad : w âˆˆ lowerBounds {x | y â‰¤ F x} := by
    intro a ha
    by_contra con'
    exact lt_irrefl _ <|
          ((ha.trans (F_mono (not_le.mp con').le)).trans (F_mono w_lt_x'.le)).trans_lt h
  exact lt_irrefl _ <| (((isGLB_sInf {x | y â‰¤ F x}).2 bad).trans (not_lt.mp con)).trans_lt x_lt_w

lemma apply_lt_of_lt_lcInv {F : R â†’ S} {y : S} {x : R} (hx : x < lcInv F y) :
    F x < y := by
  by_contra con
  exact lt_irrefl _ <| hx.trans_le (sInf_le (not_lt.mp con))

lemma lt_apply_of_rcInv_lt {F : R â†’ S} {y : S} {x : R} (hx : rcInv F y < x) :
    y < F x :=
  apply_lt_of_lt_lcInv (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) hx

/-- Note: The forward implication holds without monotonicity assumption;
see `exists_apply_lt_of_lcInv_gt`. -/
lemma lcInv_gt_iff_exists_apply_lt [DenselyOrdered R] (F_mono : Monotone F) {x : R} {y : S} :
    x < lcInv F y â†” âˆƒ x' > x, F x' < y := by
  constructor
  Â· exact exists_apply_lt_of_lcInv_gt
  Â· intro âŸ¨x', x_lt_x', hâŸ©
    apply lcInv_gt_of_exists_apply_lt F_mono x_lt_x' h

/-- The function `lcInv F` is increasing. -/
lemma lcInv_mono (F : R â†’ S) :
    Monotone (lcInv F) := by
  intro yâ‚ yâ‚‚ hy
  have key : {x | yâ‚‚ â‰¤ F x} âŠ† {x | yâ‚ â‰¤ F x} := fun x hx â†¦ le_trans hy hx
  apply sInf_le_sInf key

/-- The function `rcInv F` is increasing. -/
lemma rcInv_mono (F : R â†’ S) :
    Monotone (rcInv F) := by
  intro yâ‚ yâ‚‚ hy
  apply sSup_le_sSup fun x hx â†¦ le_trans hx hy

lemma lcInv_apply_self_le (x : R) :
    (lcInv F) (F x) â‰¤ x :=
  sInf_le <| by simpa only [Set.mem_setOf_eq] using le_rfl

lemma le_rcInv_apply_self (x : R) :
    x â‰¤ (rcInv F) (F x) :=
  @lcInv_apply_self_le Ráµ’áµˆ Sáµ’áµˆ _ _ F x

lemma le_sInf_setOf_lcInv_ge {F : R â†’ S} (x x' : R) (hx : x' < x) :
    F x' â‰¤ sInf {y | x â‰¤ lcInv F y} := by
  have aux : (lcInv F) (F x') < x := (sInf_le (by simp)).trans_lt hx
  apply le_sInf fun y hy â†¦ ?_
  simp only [mem_setOf_eq] at hy
  by_contra con
  simp only [ge_iff_le, not_le] at con
  simpa using (hy.trans (lcInv_mono F con.le)).trans_lt aux

lemma sSup_setOf_rcInv_le_le {F : R â†’ S} (x x' : R) (hx : x < x') :
    sSup {y | rcInv F y â‰¤ x} â‰¤ F x' :=
  @le_sInf_setOf_lcInv_ge Ráµ’áµˆ Sáµ’áµˆ _ _ F x x' hx

lemma sInf_setOf_lcInv_ge_ge_sSup (x : R) :
    sInf {y | x â‰¤ lcInv F y} â‰¥ sSup (F '' Iio x) := by
  apply sSup_le
  intro y âŸ¨x', hxx', hyx'âŸ©
  rw [â† hyx']
  exact le_sInf_setOf_lcInv_ge _ _ hxx'

lemma sSup_setOf_rcInv_le_le_sInf (x : R) :
    sSup {y | rcInv F y â‰¤ x} â‰¤ sInf (F '' Ioi x) :=
  @sInf_setOf_lcInv_ge_ge_sSup Ráµ’áµˆ Sáµ’áµˆ _ _ F x

lemma lcInv_ge_of_sSup_lt (x : R) (z : S) (hz : sSup (F '' Iio x) < z) :
    lcInv F z â‰¥ x := by
  have aux (x' : R) (hx : x' < x) : F x' < z := by
    apply (le_sSup ?_).trans_lt hz
    exact mem_image_of_mem F hx
  apply le_sInf
  intro x' hx'
  by_contra con'
  simpa using hx'.trans_lt (aux _ (not_le.mp con'))

lemma rcInv_le_of_lt_sInf (x : R) (z : S) (hz : z < sInf (F '' Ioi x)) :
    rcInv F z â‰¤ x :=
  @lcInv_ge_of_sSup_lt Ráµ’áµˆ Sáµ’áµˆ _ _ F x z hz

lemma ge_sInf_setOf_lcInv_ge (x : R) (z : S) (hz : sSup (F '' Iio x) < z) :
    z â‰¥ sInf {y | x â‰¤ lcInv F y} := by
  apply sInf_le
  exact lcInv_ge_of_sSup_lt _ _ hz

lemma le_sSup_setOf_rcInv_le (x : R) (z : S) (hz : z < sInf (F '' Ioi x)) :
    z â‰¤ sSup {y | rcInv F y â‰¤ x} :=
  @ge_sInf_setOf_lcInv_ge Ráµ’áµˆ Sáµ’áµˆ _ _ F x z hz

lemma sInf_setOf_lcInv_ge_le_sSup (x : R) [DenselyOrdered S] :
    sInf {y | x â‰¤ lcInv F y} â‰¤ sSup (F '' Iio x) := by
  suffices âˆ€ z, sSup (F '' Iio x) < z â†’ sInf {y | x â‰¤ lcInv F y} â‰¤ z by
    have obs : sInf {y | x â‰¤ lcInv F y} â‰¤ sInf {z | sSup (F '' Iio x) < z} :=
      le_sInf fun z hz â†¦ this _ hz
    apply obs.trans (le_of_eq ?_)
    apply sInf_eq_of_forall_ge_of_forall_gt_exists_lt
    Â· exact fun z hz â†¦ le_of_lt hz
    Â· exact fun z hz â†¦ exists_between hz
  exact ge_sInf_setOf_lcInv_ge x

lemma sSup_setOf_rcInv_le_ge_sInf (x : R) [DenselyOrdered S] :
    sSup {y | rcInv F y â‰¤ x} â‰¥ sInf (F '' Ioi x) :=
  @sInf_setOf_lcInv_ge_le_sSup Ráµ’áµˆ Sáµ’áµˆ _ _ F x _

/-- If `G` is the left-continuous pseudoinverse of `F`, then we have
`inf {y | G(y) â‰¥ x} = sup {F(x') | x' < x}`. -/
theorem sInf_setOf_lcInv_ge_eq_sSup [DenselyOrdered S] (x : R) :
    sInf {y | x â‰¤ lcInv F y} = sSup (F '' Iio x) :=
  le_antisymm (sInf_setOf_lcInv_ge_le_sSup x) (sInf_setOf_lcInv_ge_ge_sSup x)

/-- If `G` is the right-continuous pseudoinverse of `F`, then we have
`sup {y | x â‰¤ G(y)} = inf {F(x') | x' > x}`. -/
theorem sSup_setOf_rcInv_le_eq_sInf [DenselyOrdered S] (x : R) :
    sSup {y | rcInv F y â‰¤ x} = sInf (F '' Ioi x) :=
  @sInf_setOf_lcInv_ge_eq_sSup Ráµ’áµˆ Sáµ’áµˆ _ _ F _ x

/-- The double left-continuous inverse `lcInv (lcInv F)` of `F` is the left-continuous
modification `lcMod` of `F`. -/
theorem lcInv_lcInv_apply_eq_lcMod_apply [DenselyOrdered S] (x : R) :
    lcInv (lcInv F) x = lcMod F x :=
  sInf_setOf_lcInv_ge_eq_sSup _

/-- The double right-continuous inverse `rcInv (rcInv F)` of `F` is the right-continuous
modification `rcMod` of `F`. -/
theorem rcInv_rcInv_apply_eq_rcMod_apply [DenselyOrdered S] (x : R) :
    rcInv (rcInv F) x = rcMod F x :=
  sSup_setOf_rcInv_le_eq_sInf _

/-- The double left-continuous inverse `lcInv (lcInv F)` of `F` is the left-continuous
modification `lcMod` of `F`. -/
theorem lcInv_lcInv_eq_lcMod [DenselyOrdered S] :
    lcInv (lcInv F) = lcMod F := by
  ext x
  exact lcInv_lcInv_apply_eq_lcMod_apply x

/-- The double right-continuous inverse `rcInv (rcInv F)` of `F` is the right-continuous
modification `rcMod` of `F`. -/
theorem rcInv_rcInv_eq_rcMod [DenselyOrdered S] :
    rcInv (rcInv F) = rcMod F := by
  ext x
  exact rcInv_rcInv_apply_eq_rcMod_apply x

/-- The left-continuous (pseudo-)inverse is left-continuous. -/
lemma leftOrdContinuous_lcInv [DenselyOrdered R] (F_mono : Monotone F) :
    LeftOrdContinuous (lcInv F) := by
  -- TODO: Improve this proof?
  intro s lub_s h_lub_s
  refine âŸ¨?_, ?_âŸ©
  Â· simp only [mem_upperBounds, mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
    intro x x_in_s
    exact lcInv_mono _ (h_lub_s.1 x_in_s)
  Â· intro u hu
    simp only [mem_upperBounds, mem_image, forall_exists_index, and_imp,
               forall_apply_eq_imp_iffâ‚‚] at hu
    by_contra con
    simp only [not_le] at con
    obtain âŸ¨z, u_lt_z, z_ltâŸ© := exists_between con
    have obs_z : F z âˆˆ upperBounds s := by
      intro y' y'_in_s
      by_contra maybe
      simp only [not_le] at maybe
      have z_le : lcInv F y' â‰¥ z := by
        apply le_sInf
        intro x' le_Fx'
        by_contra con'
        simp only [not_le] at con'
        apply lt_irrefl _ ((le_Fx'.trans (F_mono con'.le)).trans_lt maybe)
      exact lt_irrefl _ (u_lt_z.trans_le (z_le.trans (hu _ y'_in_s)))
    have oops : F z < lub_s := by
      by_contra maybe
      simp only [not_lt] at maybe
      have le_z : z â‰¥ lcInv F lub_s := sInf_le maybe
      exact lt_irrefl _ (z_lt.trans_le le_z)
    exact lt_irrefl _ (oops.trans_le (h_lub_s.2 obs_z))

/-- The right-continuous (pseudo-)inverse is right-continuous. -/
lemma rightOrdContinuous_lcInv [DenselyOrdered R] (F_mono : Monotone F) :
    RightOrdContinuous (rcInv F) :=
  leftOrdContinuous_lcInv (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) (F := F) (fun _ _ hx â†¦ F_mono hx)

variable (F)

lemma Equiv.monotone_symm {R S : Type*} [LinearOrder R] [PartialOrder S] (Ï† : R â‰ƒ S)
    (Ï†_mono : Monotone Ï†) :
    Monotone Ï†.symm := by
  intro xâ‚ xâ‚‚ hxâ‚â‚‚
  by_contra con
  have obs := Ï†_mono (not_le.mp con).le
  simp only [Equiv.apply_symm_apply] at obs
  simp [show xâ‚ = xâ‚‚ from le_antisymm hxâ‚â‚‚ obs] at con

lemma Equiv.monotone_of_monotone_symm {R S : Type*} [PartialOrder R] [LinearOrder S] (Ï† : R â‰ƒ S)
    (symm_mono : Monotone Ï†.symm) :
    Monotone Ï† :=
  Ï†.symm.monotone_symm symm_mono

lemma comp_lcInv (F : R â†’ S) (Ï† : S â‰ƒ S) (hÏ† : Monotone Ï†) :
    lcInv (Ï† âˆ˜ F) = (lcInv F) âˆ˜ Ï†.symm := by
  ext y
  simp only [lcInv, Function.comp_apply]
  congr
  ext x
  exact âŸ¨fun h' â†¦ by simpa using (Ï†.monotone_symm hÏ†) h', fun h â†¦ by simpa using hÏ† hâŸ©

lemma symm_comp_lcInv (F : R â†’ S) (Ï† : S â‰ƒ S) (hÏ† : Monotone Ï†) :
    lcInv (Ï†.symm âˆ˜ F) = (lcInv F) âˆ˜ Ï† :=
  comp_lcInv _ _ (Ï†.monotone_symm hÏ†)

lemma comp_rcInv (F : R â†’ S) (Ï† : S â‰ƒ S) (hÏ† : Monotone Ï†) :
    rcInv (Ï† âˆ˜ F) = (rcInv F) âˆ˜ Ï†.symm :=
  comp_lcInv (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) F Ï† (fun _ _ hx â†¦ by exact hÏ† hx)

lemma symm_comp_rcInv (F : R â†’ S) (Ï† : S â‰ƒ S) (hÏ† : Monotone Ï†) :
    rcInv (Ï†.symm âˆ˜ F) = (rcInv F) âˆ˜ Ï† :=
  comp_rcInv _ _ (Ï†.monotone_symm hÏ†)

section DenselyOrdered

variable [DenselyOrdered R]

lemma LeftOrdContinuous.self_lcInv_le {F : R â†’ S} (F_lcont : LeftOrdContinuous F) (y : S) :
    F (lcInv F y) â‰¤ y := by
  apply (F_lcont (isLUB_Iio (a := (lcInv F y)))).2
  simpa [mem_upperBounds] using fun x hx â†¦ (apply_lt_of_lt_lcInv hx).le

lemma RightOrdContinuous.self_rcInv_ge {F : R â†’ S} {y : S} (F_rcont : RightOrdContinuous F) :
    y â‰¤ F (rcInv F y) :=
  LeftOrdContinuous.self_lcInv_le (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) F_rcont y

lemma RightOrdContinuous.self_lcInv_ge {F : R â†’ S} (F_rcont : RightOrdContinuous F) (y : S) :
    y â‰¤ F (lcInv F y) := by
  apply (F_rcont (isGLB_Ioi (a := (lcInv F y)))).2
  simp only [mem_lowerBounds, mem_image, mem_Ioi, forall_exists_index, and_imp,
             forall_apply_eq_imp_iffâ‚‚]
  intro x hx
  rw [lcInv, sInf_lt_iff] at hx
  obtain âŸ¨z, hz, z_lt_xâŸ© := hx
  exact hz.trans (F_rcont.mono z_lt_x.le)

lemma LeftOrdContinuous.self_rcInv_le {F : R â†’ S} (F_lcont : LeftOrdContinuous F) (y : S) :
    F (rcInv F y) â‰¤ y :=
  RightOrdContinuous.self_lcInv_ge (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) F_lcont y

end DenselyOrdered

section OrderTopology

open Topology Filter

lemma lcMod_apply_eq_self_apply' [TopologicalSpace R] [TopologicalSpace S] [OrderTopology S]
    {F : R â†’ S} (F_mono : Monotone F) {x : R}
    (hFx : ContinuousAt F x) (lt_x_neBot : (ð“[<] x).NeBot) :
    lcMod F x = F x := by
  apply le_antisymm (lcMod_apply_le_self_apply F_mono x)
  have aux : Filter.Tendsto F (ð“[<] x) (ð“ (F x)) := tendsto_nhdsWithin_of_tendsto_nhds hFx
  apply le_of_tendsto_of_frequently aux (b := lcMod F x)
  apply Eventually.frequently
  filter_upwards [self_mem_nhdsWithin] with z hz using le_lcMod_apply_of_lt F hz

lemma lcMod_apply_eq_self_apply [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [TopologicalSpace S] [OrderTopology S] {F : R â†’ S}
    (F_mono : Monotone F) {x : R} (hFx : ContinuousAt F x) (x_ne_bot : Â¬ IsMin x) :
    lcMod F x = F x := by
  apply lcMod_apply_eq_self_apply' F_mono hFx
  exact nhdsWithin_Iio_self_neBot' (Iio_nonempty.mpr x_ne_bot)

lemma rcMod_apply_eq_self_apply' [TopologicalSpace R] [TopologicalSpace S] [OrderTopology S]
    {F : R â†’ S} (F_mono : Monotone F) (x : R)
    (hFx : ContinuousAt F x) (gt_x_neBot : (ð“[>] x).NeBot) :
    rcMod F x = F x :=
  lcMod_apply_eq_self_apply' (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) (fun _ _ hx â†¦ by exact F_mono hx) hFx gt_x_neBot

lemma rcMod_apply_eq_self_apply [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [TopologicalSpace S] [OrderTopology S] {F : R â†’ S}
    (F_mono : Monotone F) (x : R) (hFx : ContinuousAt F x) (x_ne_top : Â¬ IsMax x) :
    rcMod F x = F x :=
  lcMod_apply_eq_self_apply (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) (fun _ _ hx â†¦ by exact F_mono hx) hFx x_ne_top

end OrderTopology

end pseudoinverses
