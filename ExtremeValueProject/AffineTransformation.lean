/-
Copyright (c) 2025 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤, ...
-/
import ExtremeValueProject.DegenerateCDF
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.Analysis.Normed.Field.Instances
import Mathlib.Order.OrdContinuous
import Mathlib.RingTheory.Henselian
import Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.Topology.Metrizable.CompletelyMetrizable





section affine

open Topology Filter Set

/-- Mathlib's definition of an affine map is more general, but it can be shown that an affine
map `A : ğ•œ â†’ ğ•œ` of a field `ğ•œ` is a map of the form `x â†¦ a * x + b` for some
coefficients `a b : ğ•œ`. The function `AffineMap.coefs_of_field` extracts the pair `âŸ¨a, bâŸ©`
of such coefficients from an affine map. -/
def AffineMap.coefs_of_field {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â†’áµƒ[ğ•œ] ğ•œ) : ğ•œ Ã— ğ•œ :=
    âŸ¨LinearMap.ringLmapEquivSelf ğ•œ ğ•œ ğ•œ A.linear, A 0âŸ©

/-- An affine map `A : ğ•œ â†’ ğ•œ` is of the form `x â†¦ a * x + b` for the values `a b : ğ•œ` which
are obtained by `AffineMap.coefs_of_field`. -/
lemma AffineMap.apply_eq_of_field {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â†’áµƒ[ğ•œ] ğ•œ) (x : ğ•œ) :
    A x = A.coefs_of_field.1 * x + A.coefs_of_field.2 := by
  rw [â† add_zero x]
  convert A.map_vadd 0 x
  Â· funext r
    simp [AffineMap.coefs_of_field]
  Â· simp

/-- An affine equivalence `A : ğ•œ â†’ ğ•œ` is of the form `x â†¦ a * x + b` for the values `a b : ğ•œ`
which are obtained by `AffineEquiv.toAffineMap.coefs_of_field`. -/
lemma AffineEquiv.apply_eq_of_field {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â‰ƒáµƒ[ğ•œ] ğ•œ) (x : ğ•œ) :
    A x = A.toAffineMap.coefs_of_field.1 * x + A.toAffineMap.coefs_of_field.2 := by
  rw [show A x = A.toAffineMap x from rfl]
  exact AffineMap.apply_eq_of_field A x

lemma AffineEquiv.coefs_of_field_fst_ne_zero {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â‰ƒáµƒ[ğ•œ] ğ•œ) :
    A.toAffineMap.coefs_of_field.1 â‰  0 := by
  intro maybe_zero
  have obs : A 0 = A 1 := by
    change A.toAffineMap 0 = A.toAffineMap 1
    simp_rw [A.toAffineMap.apply_eq_of_field]
    simp [maybe_zero]
  exact zero_ne_one <| A.injective obs

/-- Construct an affine map `ğ•œ â†’áµƒ[ğ•œ] ğ•œ` from coefficients `a b : ğ•œ` by the
formula `x â†¦ a * x + b`. -/
def AffineMap.mkOfCoefs_of_field {ğ•œ : Type*} [Field ğ•œ] (a b : ğ•œ) :
    ğ•œ â†’áµƒ[ğ•œ] ğ•œ where
  toFun x := a * x + b
  linear :=
    { toFun x := a * x
      map_add' x y := by ring
      map_smul' c x := by simp only [smul_eq_mul, RingHom.id_apply] ; ring }
  map_vadd' p v := by simp only [vadd_eq_add, LinearMap.coe_mk, AddHom.coe_mk] ; ring

@[simp] lemma AffineMap.coefs_of_field_fst_mkOfCoefs_of_field {ğ•œ : Type*} [Field ğ•œ] (a b : ğ•œ) :
    (AffineMap.mkOfCoefs_of_field a b).coefs_of_field.1 = a := by
  simp [AffineMap.mkOfCoefs_of_field, AffineMap.coefs_of_field]

@[simp] lemma AffineMap.coefs_of_field_snd_mkOfCoefs_of_field {ğ•œ : Type*} [Field ğ•œ] (a b : ğ•œ) :
    (AffineMap.mkOfCoefs_of_field a b).coefs_of_field.2 = b := by
  simp [AffineMap.mkOfCoefs_of_field, AffineMap.coefs_of_field]

lemma AffineMap.mkOfCoefs_of_field_apply_eq {ğ•œ : Type*} [Field ğ•œ] (a b : ğ•œ) (x : ğ•œ):
    AffineMap.mkOfCoefs_of_field a b x = a * x + b :=
  rfl

@[simp] lemma AffineMap.mkOfCoefs_of_field_eq_self
    {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â†’áµƒ[ğ•œ] ğ•œ) :
    AffineMap.mkOfCoefs_of_field A.coefs_of_field.1 A.coefs_of_field.2 = A := by
  ext x
  simp [AffineMap.apply_eq_of_field A x, AffineMap.mkOfCoefs_of_field]

def AffineEquiv.mkOfCoefs_of_field {ğ•œ : Type*} [Field ğ•œ] {a : ğ•œ} (a_ne_zero : a â‰  0) (b : ğ•œ) :
    ğ•œ â‰ƒáµƒ[ğ•œ] ğ•œ where
  toFun := AffineMap.mkOfCoefs_of_field a b
  invFun := AffineMap.mkOfCoefs_of_field aâ»Â¹ (-aâ»Â¹ * b)
  left_inv x := by
    simp [AffineMap.mkOfCoefs_of_field, mul_add, â† mul_assoc, inv_mul_cancelâ‚€ a_ne_zero]
  right_inv x := by
    simp [AffineMap.mkOfCoefs_of_field, mul_add, â† mul_assoc, mul_inv_cancelâ‚€ a_ne_zero]
  linear :=
    { toFun x := a * x
      map_add' x y := by ring
      map_smul' c x := by simp only [smul_eq_mul, RingHom.id_apply] ; ring
      invFun x := aâ»Â¹ * x
      left_inv x := by simp [â† mul_assoc, inv_mul_cancelâ‚€ a_ne_zero]
      right_inv x := by simp [â† mul_assoc, mul_inv_cancelâ‚€ a_ne_zero] }
  map_vadd' p v := by
    simp only [AffineMap.mkOfCoefs_of_field, AffineMap.coe_mk, neg_mul, vadd_eq_add,
               Equiv.coe_fn_mk, LinearEquiv.coe_mk]
    ring

@[simp] lemma AffineEquiv.mkOfCoefs_of_field_toAffineMap {ğ•œ : Type*} [Field ğ•œ]
    {a : ğ•œ} (a_ne_zero : a â‰  0) (b : ğ•œ) :
    (AffineEquiv.mkOfCoefs_of_field a_ne_zero b).toAffineMap
      = AffineMap.mkOfCoefs_of_field a b :=
  rfl

@[simp] lemma AffineEquiv.mkOfCoefs_of_field_symm_eq_mkOfCoefs_of_field
    {ğ•œ : Type*} [Field ğ•œ] {a : ğ•œ} (a_ne_zero : a â‰  0) (b : ğ•œ) :
    (AffineEquiv.mkOfCoefs_of_field a_ne_zero b).symm.toAffineMap
      = AffineMap.mkOfCoefs_of_field aâ»Â¹ (-aâ»Â¹ * b) :=
  rfl

lemma AffineEquiv.coefs_of_field_fst_mkOfCoefs_of_field {ğ•œ : Type*} [Field ğ•œ]
    {a : ğ•œ} (a_ne_zero : a â‰  0) (b : ğ•œ) :
    (AffineEquiv.mkOfCoefs_of_field a_ne_zero b).toAffineMap.coefs_of_field.1 = a := by
  simp

lemma AffineEquiv.coefs_of_field_snd_mkOfCoefs_of_field {ğ•œ : Type*} [Field ğ•œ]
    {a : ğ•œ} (a_ne_zero : a â‰  0) (b : ğ•œ) :
    (AffineEquiv.mkOfCoefs_of_field a_ne_zero b).toAffineMap.coefs_of_field.2 = b := by
  simp

lemma AffineEquiv.mkOfCoefs_of_field_apply_eq
    {ğ•œ : Type*} [Field ğ•œ] {a : ğ•œ} (a_ne_zero : a â‰  0) (b : ğ•œ) (x : ğ•œ) :
    (AffineEquiv.mkOfCoefs_of_field a_ne_zero b) x = a * x + b :=
  rfl

lemma AffineEquiv.mkOfCoefs_of_field_symm_apply_eq
    {ğ•œ : Type*} [Field ğ•œ] {a : ğ•œ} (a_ne_zero : a â‰  0) (b : ğ•œ) (x : ğ•œ) :
    (AffineEquiv.mkOfCoefs_of_field a_ne_zero b).symm x = aâ»Â¹ * x - aâ»Â¹ * b := by
  rw [show (mkOfCoefs_of_field a_ne_zero b).symm x = AffineMap.mkOfCoefs_of_field aâ»Â¹ (-aâ»Â¹ * b) x
      from rfl]
  simp only [neg_mul, AffineMap.mkOfCoefs_of_field_apply_eq]
  ring

@[simp] lemma AffineEquiv.mkOfCoefs_of_field_eq_self
    {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â‰ƒáµƒ[ğ•œ] ğ•œ) :
    AffineEquiv.mkOfCoefs_of_field (coefs_of_field_fst_ne_zero A) A.toAffineMap.coefs_of_field.2
      = A := by
  ext x
  rw [show A x = A.toAffineMap x from rfl, AffineMap.apply_eq_of_field A.toAffineMap x]
  simp [mkOfCoefs_of_field, AffineMap.mkOfCoefs_of_field]

/-- The inverse `Aâ»Â¹` of an affine map `A : ğ•œ â†’ ğ•œ` is of the form `x â†¦ a * x + b`
is `y â†¦ Î± * x + Î²` where `Î± = aâ»Â¹` and `Î² = - aâ»Â¹ * b`. -/
lemma AffineMap.mkOfCoefs_of_field_eq_inv {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â‰ƒáµƒ[ğ•œ] ğ•œ) :
    (AffineEquiv.mkOfCoefs_of_field (inv_ne_zero A.coefs_of_field_fst_ne_zero)
      (-(A.toAffineMap.coefs_of_field.1)â»Â¹ * A.toAffineMap.coefs_of_field.2))
      = Aâ»Â¹ := by
  ext x
  simp only [show Aâ»Â¹ = A.symm from rfl, neg_mul, AffineEquiv.mkOfCoefs_of_field_apply_eq]
  nth_rw 4 [â† AffineEquiv.mkOfCoefs_of_field_eq_self A]
  rw [AffineEquiv.mkOfCoefs_of_field_symm_apply_eq]
  ring

/-- The inverse `Aâ»Â¹` of an affine map `A : ğ•œ â†’ ğ•œ` is of the form `x â†¦ a * x + b`
is `y â†¦ Î± * x + Î²` where `Î± = aâ»Â¹`. -/
lemma AffineEquiv.inv_coefs_of_field_fst {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â‰ƒáµƒ[ğ•œ] ğ•œ) :
    (Aâ»Â¹).toAffineMap.coefs_of_field.1 = (A.toAffineMap.coefs_of_field.1)â»Â¹ := by
  simp [â† AffineMap.mkOfCoefs_of_field_eq_inv A]

/-- The inverse `Aâ»Â¹` of an affine map `A : ğ•œ â†’ ğ•œ` is of the form `x â†¦ a * x + b`
is `y â†¦ Î± * x + Î²` where `Î² = - aâ»Â¹ * b`. -/
lemma AffineMap.inv_coefs_of_field_fst {ğ•œ : Type*} [Field ğ•œ] (A : ğ•œ â‰ƒáµƒ[ğ•œ] ğ•œ) (x : ğ•œ) :
    (Aâ»Â¹).toAffineMap.coefs_of_field.2
      = -(A.toAffineMap.coefs_of_field.1)â»Â¹ * A.toAffineMap.coefs_of_field.2 := by
  simp [â† AffineMap.mkOfCoefs_of_field_eq_inv A]

/-- An affine isomorphism `â„ â†’ â„` to be orientation preserving if its linear coefficient
is positive. -/
def AffineEquiv.IsOrientationPreserving (A : â„ â‰ƒáµƒ[â„] â„) : Prop :=
  0 < A.toAffineMap.coefs_of_field.1

/-- An affine isomorphism `â„ â†’ â„` to be orientation preserving if and only if it is
an increasing function. -/
lemma AffineEquiv.isOrientationPreserving_iff_mono (A : â„ â‰ƒáµƒ[â„] â„) :
    A.IsOrientationPreserving â†” Monotone (fun x â†¦ A x) := by
  unfold IsOrientationPreserving
  set a := A.toAffineMap.coefs_of_field.1
  set b := A.toAffineMap.coefs_of_field.2
  have in_other_words (x) : A x = a * x + b := AffineMap.apply_eq_of_field A x
  simp_rw [in_other_words]
  constructor
  Â· intro a_pos
    intro x y x_le_y
    simpa using (mul_le_mul_iff_of_pos_left a_pos).mpr x_le_y
  Â· intro mono
    have key := mono zero_le_one
    simp only [mul_zero, zero_add, mul_one, le_add_iff_nonneg_left] at key
    have a_nonzero : a â‰  0 := by exact coefs_of_field_fst_ne_zero A
    exact lt_of_le_of_ne' key a_nonzero

-- TODO: Generalize to canonically linearly ordered fields?
/-- The subgroup of affine isomorphishs â„ â†’ â„ which are orientation preserving. -/
noncomputable def orientationPreservingAffineEquiv : Subgroup (â„ â‰ƒáµƒ[â„] â„) where
  carrier := AffineEquiv.IsOrientationPreserving
  mul_mem' := by
    simp_rw [mem_def, AffineEquiv.isOrientationPreserving_iff_mono]
    exact Monotone.comp
  one_mem' := Real.zero_lt_one
  inv_mem' := by
    intro x hx
    apply AffineEquiv.inv_coefs_of_field_fst x â–¸ Right.inv_pos.mpr hx

/-- Orientation preserving affine isomorphisms â„ â†’ â„ are continuous. -/
lemma orientationPreservingAffineEquiv.continuous (A : orientationPreservingAffineEquiv) :
    Continuous (A : â„ â†’ â„) := by
  apply (AffineMap.continuous_iff (R := â„) (E := â„) (F := â„) (f := A)).mpr
  exact LinearMap.continuous_of_finiteDimensional _

lemma orientationPreservingAffineEquiv.monotone (A : orientationPreservingAffineEquiv) :
    Monotone (A : â„ â†’ â„) :=
  (AffineEquiv.isOrientationPreserving_iff_mono ..).mp A.prop

end affine



section affine_transform_of_cdf

namespace CumulativeDistributionFunction

/-- The action of orientation preserving affine isomorphisms on cumulative distribution
functions, so that for `A : orientationPreservingAffineEquiv` and
`F : CumulativeDistributionFunction` we have `(A â€¢ F)(x) = F(Aâ»Â¹ x)`. -/
noncomputable def affineTransform
    (F : CumulativeDistributionFunction) (A : orientationPreservingAffineEquiv) :
    CumulativeDistributionFunction where
  toFun := fun x â†¦ F (Aâ»Â¹.val x)
  mono' := F.mono'.comp (orientationPreservingAffineEquiv.monotone Aâ»Â¹)
  right_continuous' := by
    have orientationPreservingAffineEquiv_image_Ici (B : orientationPreservingAffineEquiv) (x : â„) :
        Set.Ici (B.val x) = B.val '' (Set.Ici x) := by
      have B_Binv (z) : B.val (B.valâ»Â¹ z) = z := (AffineEquiv.apply_eq_iff_eq_symm_apply _).mpr rfl
      have Binv_B (z) : B.valâ»Â¹ (B.val z) = z := (AffineEquiv.apply_eq_iff_eq_symm_apply _).mpr rfl
      have B_mono : Monotone (B.val) := orientationPreservingAffineEquiv.monotone B
      have Binv_mono : Monotone (Bâ»Â¹.val) := orientationPreservingAffineEquiv.monotone Bâ»Â¹
      ext z
      refine âŸ¨fun hBz â†¦ ?_, fun hBiz â†¦ ?_âŸ©
      Â· refine âŸ¨B.valâ»Â¹ z, by simpa [Binv_B] using Binv_mono hBz, B_Binv _âŸ©
      Â· obtain âŸ¨w, hw, Bw_eqâŸ© := hBiz
        simpa [â† Bw_eq] using B_mono hw
    intro x
    exact (F.right_continuous (Aâ»Â¹.val x)).comp
      (orientationPreservingAffineEquiv.continuous Aâ»Â¹).continuousWithinAt
      (orientationPreservingAffineEquiv_image_Ici Aâ»Â¹ x â–¸ Set.mapsTo_image Aâ»Â¹.val (Set.Ici x))
  tendsto_atTop := by
    apply Filter.Tendsto.comp F.tendsto_atTop
    Â· refine Monotone.tendsto_atTop_atTop ?A_inv_is_monotone ?A_inv_is_top_unbounded
      Â· exact orientationPreservingAffineEquiv.monotone Aâ»Â¹
      Â· intro b
        use A.val b
        rw [InvMemClass.coe_inv,AffineEquiv.inv_def,AffineEquiv.symm_apply_apply]
  tendsto_atBot := by
    apply Filter.Tendsto.comp F.tendsto_atBot
    Â· refine Monotone.tendsto_atBot_atBot ?A_inv_is_monotone' ?A_inv_is_bottom_unbounded
      Â· exact orientationPreservingAffineEquiv.monotone Aâ»Â¹
      Â· intro b
        use A.val b
        rw [InvMemClass.coe_inv,AffineEquiv.inv_def,AffineEquiv.symm_apply_apply]

@[simp] lemma affineTransform_apply_eq
    (F : CumulativeDistributionFunction) (A : orientationPreservingAffineEquiv) (x : â„):
    (F.affineTransform A) x = F ((Aâ»Â¹ : â„ â‰ƒáµƒ[â„] â„) x) := rfl

lemma affineTransform_mul_apply_eq_comp
    (F : CumulativeDistributionFunction) (A B : orientationPreservingAffineEquiv) :
    F.affineTransform (A * B) = (F.affineTransform B).affineTransform A := rfl

@[simp] lemma affineTransform_one_apply (F : CumulativeDistributionFunction) :
    F.affineTransform 1 = F := rfl

/-- The action of orientation preserving affine isomorphisms on cumulative distribution
functions, so that for `A : orientationPreservingAffineEquiv` and
`F : CumulativeDistributionFunction` we have `(A â€¢ F)(x) = F(Aâ»Â¹ x)`. -/
noncomputable instance instMulActionOrientationPreservingAffineEquiv :
    MulAction orientationPreservingAffineEquiv CumulativeDistributionFunction where
  smul A F := F.affineTransform A
  one_smul _ := rfl
  mul_smul _ _ _ := rfl

@[simp] lemma mulAction_apply_eq
    (F : CumulativeDistributionFunction) (A : orientationPreservingAffineEquiv) (x : â„):
    (A â€¢ F) x = F ((Aâ»Â¹ : â„ â‰ƒáµƒ[â„] â„) x) := rfl

-- Lemma: If X is a â„-valued random variable with c.d.f. F, then the c.d.f. of A â€¢ X is A â€¢ F.

/-- An affine transform of a c.d.f. is degenerate iff the c.d.f. itself is degenerate. -/
lemma affine_isDegenerate_iff
    (F : CumulativeDistributionFunction) (A : orientationPreservingAffineEquiv) :
    (A â€¢ F).IsDegenerate â†” F.IsDegenerate := Iff.symm A.val.toEquiv.forall_congr_left

/-- An affine transform of a c.d.f. is continuious at `A x` if the c.d.f. itself is continuous
at `x`. -/
lemma affine_continuousAt_of_continuousAt
    {F : CumulativeDistributionFunction} {x : â„} (F_cont : ContinuousAt F x)
    (A : orientationPreservingAffineEquiv) :
    ContinuousAt (A â€¢ F) ((A : â„ â‰ƒáµƒ[â„] â„) x) := by
  have ha := (A : â„ â‰ƒáµƒ[â„] â„)â»Â¹.continuous_of_finiteDimensional
  let f := fun x â†¦ (A : â„ â‰ƒáµƒ[â„] â„)â»Â¹ x
  rw [show (A â€¢ F).toStieltjesFunction = F âˆ˜ f from rfl]
  have h_simp : f ((A : â„ â‰ƒáµƒ[â„] â„) x) = x := (AffineEquiv.apply_eq_iff_eq_symm_apply _).mpr rfl
  rw[â† h_simp] at F_cont
  exact ContinuousAt.comp F_cont ha.continuousAt

/-- An affine transform of a c.d.f. is continuious at `A x` if and only if the c.d.f. itself is
continuous at `x`. -/
lemma affine_continuousAt_iff
    (F : CumulativeDistributionFunction) (A : orientationPreservingAffineEquiv) (x : â„) :
    ContinuousAt (A â€¢ F) x â†” ContinuousAt F ((Aâ»Â¹ : â„ â‰ƒáµƒ[â„] â„) x) := by
  constructor
  Â· intro AF_cont
    convert affine_continuousAt_of_continuousAt AF_cont Aâ»Â¹
    simp
  Â· intro F_cont
    convert affine_continuousAt_of_continuousAt F_cont A
    exact (@AffineEquiv.apply_symm_apply â„ â„ â„ â„ â„ _ _ _ _ _ _ _ A x).symm

end CumulativeDistributionFunction

section extend

/-- Extend an affine map `â„ â†’ â„` to `[-âˆ,+âˆ] â†’ [-âˆ,+âˆ]`. -/
noncomputable def AffineMap.extend (A : â„ â†’áµƒ[â„] â„) (x : EReal) : EReal :=
  match x with
  | âŠ¥ => if A.coefs_of_field.1 > 0 then âŠ¥
      else if A.coefs_of_field.1 < 0 then âŠ¤
      else A.coefs_of_field.2
  | âŠ¤ => if A.coefs_of_field.1 > 0 then âŠ¤
      else if A.coefs_of_field.1 < 0 then âŠ¥
      else A.coefs_of_field.2
  | some (some Î¾) => A Î¾

lemma AffineMap.leftOrdContinuous (A : â„ â†’áµƒ[â„] â„) :
    LeftOrdContinuous A := by
  sorry -- **Issue #7** (Rmk: This should go via a lemma that is being PRd to Mathlib)

lemma AffineMap.rightOrdContinuous (A : â„ â†’áµƒ[â„] â„) :
    RightOrdContinuous A := by
  sorry -- **Issue #7** (Rmk: This should go via a lemma that is being PRd to Mathlib)

lemma AffineMap.leftOrdContinuous_extend (A : â„ â†’áµƒ[â„] â„) :
    LeftOrdContinuous A.extend := by
  sorry -- **Issue #7**

lemma AffineMap.rightOrdContinuous_extend (A : â„ â†’áµƒ[â„] â„) :
    RightOrdContinuous A.extend := by
  sorry -- **Issue #7**

lemma AffineEquiv.extend_bot' (A : â„ â‰ƒáµƒ[â„] â„) :
    A.toAffineMap.extend âŠ¥ =
      if 0 < A.toAffineMap.coefs_of_field.1 then âŠ¥ else âŠ¤ := by
  have obs : A.toAffineMap.coefs_of_field.1 â‰  0 :=
    coefs_of_field_fst_ne_zero A
  by_cases hA : 0 < A.toAffineMap.coefs_of_field.1
  Â· simp [AffineMap.extend, hA]
  Â· simp only [ne_eq, not_lt] at *
    have hA' : A.toAffineMap.coefs_of_field.1 < 0 := lt_of_le_of_ne hA obs
    simp [AffineMap.extend, hA']

lemma AffineEquiv.extend_top' (A : â„ â‰ƒáµƒ[â„] â„) :
    A.toAffineMap.extend âŠ¤ =
      if 0 < A.toAffineMap.coefs_of_field.1 then âŠ¤ else âŠ¥ := by
  have obs : A.toAffineMap.coefs_of_field.1 â‰  0 :=
    coefs_of_field_fst_ne_zero A
  by_cases hA : 0 < A.toAffineMap.coefs_of_field.1
  Â· simp [AffineMap.extend, hA]
  Â· simp only [ne_eq, not_lt] at *
    have hA' : A.toAffineMap.coefs_of_field.1 < 0 := lt_of_le_of_ne hA obs
    simp [AffineMap.extend, hA']

--lemma AffineEquiv.extend_ofReal' (A : â„ â‰ƒáµƒ[â„] â„) (x : â„) :
--    A.toAffineMap.extend x = A x :=
--  rfl

lemma AffineEquiv.extend_symm_cancel (A : â„ â‰ƒáµƒ[â„] â„) (x : EReal) :
    A.symm.toAffineMap.extend (A.toAffineMap.extend x) = x := by
  simp [AffineMap.extend]
  by_cases hA : 0 < A.toAffineMap.coefs_of_field.1
  case' pos =>
    have : 0 < A.symm.toAffineMap.coefs_of_field.1 := by
      rw [show A.symm = Aâ»Â¹ from rfl, inv_coefs_of_field_fst, inv_pos]
      exact hA
    simp [hA, this]
  case' neg =>
    rw [not_lt] at hA
    have obs : A.toAffineMap.coefs_of_field.1 â‰  0 :=
      coefs_of_field_fst_ne_zero A
    have hA' : A.toAffineMap.coefs_of_field.1 < 0 := lt_of_le_of_ne hA obs
    have : A.symm.toAffineMap.coefs_of_field.1 < 0 := by
      rw [show A.symm = Aâ»Â¹ from rfl, inv_coefs_of_field_fst, inv_neg'']
      exact hA'
    simp [hA', this, lt_asymm]
  all_goals split
  all_goals
  rename_i h
  split at h <;> first | rfl | cases h
  all_goals
  rw [symm_apply_apply]
  rfl

/-- Extend an affine equivalence `â„ â†’ â„` to and equivalence `[-âˆ,+âˆ] â†’ [-âˆ,+âˆ]`. -/
noncomputable def AffineEquiv.extend (A : â„ â‰ƒáµƒ[â„] â„) : EReal â‰ƒ EReal where
  toFun := A.toAffineMap.extend
  invFun := A.symm.toAffineMap.extend
  left_inv := extend_symm_cancel A
  right_inv := extend_symm_cancel A.symm

@[simp] lemma AffineEquiv.extend_bot (A : â„ â‰ƒáµƒ[â„] â„) :
    A.extend âŠ¥ = if 0 < A.toAffineMap.coefs_of_field.1 then âŠ¥ else âŠ¤ :=
  AffineEquiv.extend_bot' A

@[simp] lemma AffineEquiv.extend_top (A : â„ â‰ƒáµƒ[â„] â„) :
    A.extend âŠ¤ = if 0 < A.toAffineMap.coefs_of_field.1 then âŠ¤ else âŠ¥ :=
  AffineEquiv.extend_top' A

@[simp] lemma AffineEquiv.extend_ofReal (A : â„ â‰ƒáµƒ[â„] â„) (x : â„):
    A.extend x = A x :=
  rfl

@[simp] lemma AffineEquiv.extend_symm (A : â„ â‰ƒáµƒ[â„] â„) :
    A.extend.symm = A.symm.extend := by
  rfl

end extend

end affine_transform_of_cdf
