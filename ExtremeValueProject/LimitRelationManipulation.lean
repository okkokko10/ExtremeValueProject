/-
Copyright (c) 2025 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤, ...
-/
import ExtremeValueProject.AffineTransformation
import ExtremeValueProject.TransformedCDF
import Mathlib

open Filter Set Metric Topology Asymptotics

open scoped Topology

section generalities

-- TODO: Should something like this be in Mathlib?
lemma NormedSpace.tendsto_zero_iff_isLittleO_one {Î¹ : Type*} {L : Filter Î¹}
    {E : Type*} [SeminormedAddCommGroup E] [NormedSpace â„ E] {f : Î¹ â†’ E} :
    L.Tendsto f (ğ“ 0) â†” (fun i â†¦ f i) =o[L] (fun _ â†¦ (1 : â„)) := by
  simp only [Metric.tendsto_nhds, gt_iff_lt, dist_zero_right, isLittleO_iff, norm_one, mul_one]
  constructor
  Â· intro h_tends Îµ Îµ_pos
    filter_upwards [h_tends Îµ Îµ_pos] with i hi using hi.le
  Â· intro h_littleO Îµ Îµ_pos
    filter_upwards [h_littleO (c := Îµ / 2) (by linarith)] with i hi
    exact lt_of_le_of_lt hi (by linarith)

-- TODO: Should something like this be in Mathlib?
lemma NormedSpace.tendsto_of_tendsto_of_sub_isLittleO_one {Î¹ : Type*} {L : Filter Î¹}
    {E : Type*} {v : E} [SeminormedAddCommGroup E] [NormedSpace â„ E] {f g : Î¹ â†’ E}
    (hf : L.Tendsto f (ğ“ v)) (hfg : (fun i â†¦ f i - g i) =o[L] (fun _ â†¦ (1 : â„))) :
    L.Tendsto g (ğ“ v) := by
  apply tendsto_sub_nhds_zero_iff.mp
  have hfv : L.Tendsto (fun i â†¦ f i - v) (ğ“ 0) := tendsto_sub_nhds_zero_iff.mpr hf
  rw [tendsto_zero_iff_isLittleO_one] at hfv
  simpa only [sub_sub_sub_cancel_left, isLittleO_one_iff] using hfv.sub hfg

-- TODO: Should something like this be in Mathlib?
-- Or maybe this is too specialized?
-- Mathlib already has `tendsto_sub_nhds_zero_iff`, but this conbines an `IsLittleO` spelling.
lemma NormedSpace.tendsto_iff_sub_const_isLittleO_one {Î¹ : Type*} {L : Filter Î¹}
    {E : Type*} {v : E} [SeminormedAddCommGroup E] [NormedSpace â„ E] {f : Î¹ â†’ E} :
    L.Tendsto f (ğ“ v) â†” (fun i â†¦ f i - v) =o[L] (fun _ â†¦ (1 : â„)) := by
  simpa [â† tendsto_zero_iff_isLittleO_one] using tendsto_sub_nhds_zero_iff.symm

end generalities


section limit_relation_manipulation_lemmas

lemma eventually_mul_norm_lt_of_tendsto_smul_of_norm_lt {Î¹ : Type*} {L : Filter Î¹}
    {E : Type*} [SeminormedAddCommGroup E] [NormedSpace â„ E] {v : E}
    (f : â„ â†’ E) {m : Î¹ â†’ â„}
    {a : Î¹ â†’ â„} (ha : L.Tendsto (fun i â†¦ m i â€¢ (f (a i))) (ğ“ v)) {c : â„} (hc : â€–vâ€– < c) :
    âˆ€á¶  i in L, |m i| * â€–f (a i)â€– < c := by
  have norm_lim : L.Tendsto (fun i â†¦ â€–m i â€¢ (f (a i))â€–) (ğ“ â€–vâ€–) := by
    have obs := continuous_norm.continuousAt (x := v)
    simp only [ContinuousAt] at obs
    exact obs.comp ha
  simp only [norm_smul, Real.norm_eq_abs] at norm_lim
  have : Iio c âˆˆ ğ“ â€–vâ€– := Iio_mem_nhds (by linarith)
  filter_upwards [norm_lim this] with i hi using hi

lemma tendsto_norm_apply_zero_of_tendsto_atTop_of_tendsto_smul {Î¹ : Type*} {L : Filter Î¹}
    {E : Type*} [SeminormedAddCommGroup E] [NormedSpace â„ E] {v : E}
    (f : â„ â†’ E) {m : Î¹ â†’ â„} (m_to_infty : L.Tendsto m atTop)
    {a : Î¹ â†’ â„} (ha : L.Tendsto (fun i â†¦ m i â€¢ (f (a i))) (ğ“ v)) :
    L.Tendsto (fun i â†¦ â€–f (a i)â€–) (ğ“ 0) := by
  have bdd : L.Eventually (fun i â†¦ |m i| * â€–f (a i)â€– < â€–vâ€– + 1) :=
    eventually_mul_norm_lt_of_tendsto_smul_of_norm_lt _ ha (by linarith)
  rw [Metric.tendsto_nhds]
  intro Îµ Îµ_pos
  have unbdd : L.Eventually (fun i â†¦ m i â‰¥ (â€–vâ€– + 1) * Îµâ»Â¹) := by
    exact Tendsto.eventually_ge_atTop m_to_infty ((â€–vâ€– + 1) * Îµâ»Â¹)
  have unbdd' : L.Eventually (fun i â†¦ |m i| â‰¥ (â€–vâ€– + 1) * Îµâ»Â¹) := by
    filter_upwards [unbdd] with i hi using le_trans hi (le_abs_self (m i))
  filter_upwards [bdd, unbdd'] with i hib hiu
  simp only [dist_zero_right, norm_norm]
  have mi_pos : 0 < |m i| := by
    linarith [show 0 < (â€–vâ€– + 1) * Îµâ»Â¹ from mul_pos (by positivity) (Right.inv_pos.mpr Îµ_pos)]
  apply lt_of_lt_of_le (show â€–f (a i)â€– < (â€–vâ€– + 1) / |m i| from (lt_div_iffâ‚€' mi_pos).mpr hib)
  rw [div_le_iffâ‚€ mi_pos]
  exact (div_le_iffâ‚€' Îµ_pos).mp hiu

lemma tendsto_zero_of_tendsto_atTop_of_tendsto_smul {Î¹ : Type*} {L : Filter Î¹}
    {E : Type*} [SeminormedAddCommGroup E] [NormedSpace â„ E] {v : E}
    (f : â„ â†’ E) {s : Set â„} (hs : âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ x âˆˆ s, â€–f xâ€– < Îµ â†’ x âˆˆ Metric.ball 0 Î´)
    {m : Î¹ â†’ â„} (m_to_infty : L.Tendsto m atTop)
    {a : Î¹ â†’ â„} (a_in_s : âˆ€á¶  i in L, a i âˆˆ s)
    (ha : L.Tendsto (fun i â†¦ m i â€¢ (f (a i))) (ğ“ v)) :
    Tendsto a L (ğ“ 0) := by
  have vanishing := tendsto_norm_apply_zero_of_tendsto_atTop_of_tendsto_smul f m_to_infty ha
  have vanishing' : Tendsto (fun i => f (a i)) L (ğ“ 0) :=
    tendsto_zero_iff_norm_tendsto_zero.mpr vanishing
  simp only [mem_ball, dist_zero_right, Real.norm_eq_abs, Metric.tendsto_nhds, norm_norm]
    at vanishing' hs âŠ¢
  intro Î´ Î´_pos
  obtain âŸ¨Îµ, Îµ_pos, hÎµâŸ© := hs Î´ Î´_pos
  filter_upwards [a_in_s, vanishing' Îµ Îµ_pos] with i his hif using hÎµ _ his hif

lemma sub_smul_deriv_isLittleO_of_tendsto_atTop_of_tendsto_smul_apply {Î¹ : Type*} {L : Filter Î¹}
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {v : E}
    (f : â„ â†’ E) (hf : f 0 = 0) {s : Set â„}
    (hs : âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ x âˆˆ s, â€–f xâ€– < Îµ â†’ x âˆˆ Metric.ball 0 Î´)
    {m : Î¹ â†’ â„} (m_to_infty : L.Tendsto m atTop) {a : Î¹ â†’ â„} (a_in_s : âˆ€á¶  i in L, a i âˆˆ s)
    (ha : L.Tendsto (fun i â†¦ m i â€¢ (f (a i))) (ğ“ v)) {D : E} (hDf : HasDerivAt f D 0) :
    (fun i â†¦ f (a i) - a i â€¢ D) =o[L] a := by
  have a_lim : Tendsto a L (ğ“ 0) :=
    tendsto_zero_of_tendsto_atTop_of_tendsto_smul f hs m_to_infty a_in_s ha
  simp only [hasDerivAt_iff_hasFDerivAt, HasFDerivAt, hasFDerivAtFilter_iff_isLittleOTVS, hf,
             sub_zero, ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply] at hDf
  rw [isLittleOTVS_iff_isLittleO] at hDf
  rw [isLittleO_iff] at hDf âŠ¢
  intro c c_pos
  specialize hDf c_pos
  exact Eventually.filter_mono (tendsto_iff_comap.mp a_lim) (hDf.comap a)

lemma eventually_norm_apply_ge_mul_self_of_tendsto_atTop_of_tendsto_smul_apply
    {Î¹ : Type*} {L : Filter Î¹} {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {v : E}
    (f : â„ â†’ E) (hf : f 0 = 0) {s : Set â„}
    (hs : âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ x âˆˆ s, â€–f xâ€– < Îµ â†’ x âˆˆ Metric.ball 0 Î´)
    {m : Î¹ â†’ â„} (m_to_infty : L.Tendsto m atTop)
    {a : Î¹ â†’ â„} (a_in_s : âˆ€á¶  i in L, a i âˆˆ s)
    (ha : L.Tendsto (fun i â†¦ m i â€¢ (f (a i))) (ğ“ v)) {D : E}
    {C : â„} (C_lt_D : C < â€–Dâ€–) (hDf : HasDerivAt f D 0) :
    âˆ€á¶  i in L, â€–f (a i)â€– â‰¥ |a i| * C := by
  have key : (fun i â†¦ f (a i) - a i â€¢ D) =o[L] a :=
    sub_smul_deriv_isLittleO_of_tendsto_atTop_of_tendsto_smul_apply _ hf hs m_to_infty a_in_s ha hDf
  rw [isLittleO_iff] at key
  filter_upwards [key (c := â€–Dâ€– - C) (by linarith)] with i hi
  have est : â€–f (a i)â€– â‰¥ â€–a i â€¢ Dâ€– - â€–f (a i) - a i â€¢ Dâ€– := by
    rw [show â€–f (a i)â€– = â€–a i â€¢ D + (f (a i) - a i â€¢ D)â€– by simp]
    exact norm_sub_le_norm_add (a i â€¢ D) (f (a i) - a i â€¢ D)
  have aux := sub_le_sub (le_refl â€–a i â€¢ Dâ€–) hi
  simp only [norm_smul, mul_comm _ â€–a iâ€–, â† mul_sub] at aux
  simp only [Real.norm_eq_abs, sub_sub_cancel] at aux
  simp only [norm_smul, Real.norm_eq_abs, ge_iff_le, tsub_le_iff_right] at est
  exact aux.trans <| (sub_le_sub est (le_refl _)).trans (by simp)

lemma mul_isBigO_one_of_tendsto_atTop_of_tendsto_smul_apply {Î¹ : Type*} {L : Filter Î¹} [NeBot L]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {v : E}
    (f : â„ â†’ E) (hf : f 0 = 0) {s : Set â„}
    (hs : âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ x âˆˆ s, â€–f xâ€– < Îµ â†’ x âˆˆ Metric.ball 0 Î´)
    {m : Î¹ â†’ â„} (m_to_infty : L.Tendsto m atTop)
    {a : Î¹ â†’ â„} (a_in_s : âˆ€á¶  i in L, a i âˆˆ s)
    (ha : L.Tendsto (fun i â†¦ m i â€¢ (f (a i))) (ğ“ v)) {D : E} (D_ne_zero : D â‰  0)
    (hDf : HasDerivAt f D 0) :
    (fun i â†¦ m i * a i) =O[L] fun _ â†¦ (1 : â„) := by
  have norm_D_pos : 0 < â€–Dâ€– := norm_pos_iff.mpr D_ne_zero
  have aux : âˆ€á¶  i in L, â€–f (a i)â€– â‰¥ |a i| * (â€–Dâ€–/2) :=
    eventually_norm_apply_ge_mul_self_of_tendsto_atTop_of_tendsto_smul_apply _ hf hs
      m_to_infty a_in_s ha (show â€–Dâ€–/2 < â€–Dâ€– by linarith) hDf
  have mfa_isBigO := Tendsto.isBigO_one â„ ha
  simp only [isBigO_iff, norm_one, mul_one, norm_mul, Real.norm_eq_abs] at mfa_isBigO âŠ¢
  obtain âŸ¨C, hCâŸ© := mfa_isBigO
  have C_nn : 0 â‰¤ C := by
    obtain âŸ¨j, hjâŸ© := hC.exists
    exact (norm_nonneg _).trans hj
  use C * (2 * â€–Dâ€–â»Â¹)
  filter_upwards [aux, hC] with i hiz hiw
  simp only [norm_smul, Real.norm_eq_abs] at hiw
  have hiz' := (@le_div_iffâ‚€ â„ _ _ _ |a i| â€–f (a i)â€– (â€–Dâ€– / 2) _ (by linarith)).mpr hiz
  simp only [div_eq_mul_inv, mul_inv_rev, inv_inv] at hiz'
  apply (mul_le_mul (le_refl |m i|) hiz' (abs_nonneg (a i)) (abs_nonneg (m i))).trans
  rw [â† mul_assoc]
  apply mul_le_mul hiw (le_refl (2 * â€–Dâ€–â»Â¹)) _ C_nn
  exact mul_nonneg zero_le_two <| inv_nonneg.mpr (norm_nonneg D)

lemma tendsto_mul_smul_deriv_of_tendsto_atTop_of_tendsto_smul_apply
    {Î¹ : Type*} {L : Filter Î¹} [NeBot L] {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {v : E} (f : â„ â†’ E) (hf : f 0 = 0) {s : Set â„}
    (hs : âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ x âˆˆ s, â€–f xâ€– < Îµ â†’ x âˆˆ Metric.ball 0 Î´)
    {m : Î¹ â†’ â„} (m_to_infty : L.Tendsto m atTop)
    {a : Î¹ â†’ â„} (a_in_s : âˆ€á¶  i in L, a i âˆˆ s)
    (ha : L.Tendsto (fun i â†¦ m i â€¢ (f (a i))) (ğ“ v)) {D : E} (D_ne_zero : D â‰  0)
    (hDf : HasDerivAt f D 0) :
    L.Tendsto (fun i â†¦ (m i * a i) â€¢ D) (ğ“ v) := by
  have obs : (fun i â†¦ f (a i) - a i â€¢ D) =o[L] a :=
    sub_smul_deriv_isLittleO_of_tendsto_atTop_of_tendsto_smul_apply _ hf hs m_to_infty a_in_s ha hDf
  have ma_isBigO : (fun i â†¦ m i * a i) =O[L] fun _ â†¦ (1 : â„) :=
    mul_isBigO_one_of_tendsto_atTop_of_tendsto_smul_apply f hf hs m_to_infty a_in_s ha D_ne_zero hDf
  have keyâ‚ : (fun i â†¦ (m i) â€¢ f (a i) - v) =o[L] (fun i â†¦ (1 : â„)) :=
    NormedSpace.tendsto_iff_sub_const_isLittleO_one.mp ha
  have keyâ‚‚ : (fun i â†¦ (m i) â€¢ f (a i) - m i â€¢ (a i â€¢ D)) =o[L] (fun i â†¦ (1 : â„)) := by
    have keyâ‚ƒ := (isBigO_refl m L).smul_isLittleO obs
    simp only [smul_sub, smul_eq_mul] at keyâ‚ƒ
    exact keyâ‚ƒ.trans_isBigO ma_isBigO
  have key := IsLittleO.sub keyâ‚ keyâ‚‚
  simp only [sub_sub_sub_cancel_left, isLittleO_one_iff, tendsto_sub_nhds_zero_iff] at key
  convert key using 1
  ext i
  exact mul_smul (m i) (a i) D

lemma tendsto_smul_self_iff_tendsto_of_ne_zero {Î¹ : Type*} {L : Filter Î¹} [NeBot L]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {c : â„} {v : E} (v_ne_zero : v â‰  0) {r : Î¹ â†’ â„} :
    L.Tendsto (fun i â†¦ r i â€¢ v) (ğ“ (c â€¢ v)) â†” L.Tendsto r (ğ“ c) := by
  constructor
  Â· intro rv_lim
    obtain âŸ¨Ï†, hÏ†vâŸ© : âˆƒ (Ï† : E â†’L[â„] â„), Ï† v = 1 := SeparatingDual.exists_eq_one v_ne_zero
    have hb : ContinuousAt Ï† (c â€¢ v) := Ï†.continuous.continuousAt
    rw [ContinuousAt] at hb
    have key : Tendsto (fun i â†¦ Ï† (r i â€¢ v)) L (ğ“ (Ï† (c â€¢ v))) := hb.comp rv_lim
    simpa only [map_smul, hÏ†v, smul_eq_mul, mul_one] using key
  Â· intro r_lim
    have key : ContinuousAt (fun (t : â„) â†¦ t â€¢ v) c :=
      (continuous_smul.comp (Continuous.prodMk_left v)).continuousAt
    simp only [ContinuousAt, one_smul] at key
    exact key.comp r_lim

lemma tendsto_smul_apply_smul_deriv_of_tendsto_atTop_of_tendsto_mul
    {Î¹ : Type*} {L : Filter Î¹} [NeBot L] {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    (f : â„ â†’ E) (hf : f 0 = 0) {s : Set â„} {m : Î¹ â†’ â„} (m_to_infty : L.Tendsto m atTop)
    {a : Î¹ â†’ â„} (a_in_s : âˆ€á¶  i in L, a i âˆˆ s) {c : â„}
    (hma : L.Tendsto (fun i â†¦ m i * a i) (ğ“ c)) {D : E} (hDf : HasDerivAt f D 0) :
    L.Tendsto (fun i â†¦ m i â€¢ (f (a i))) (ğ“ (c â€¢ D)) := by
  have hDfa : (fun i â†¦ f (a i) - a i â€¢ D) =o[L] a := by
    have a_lim : L.Tendsto a (ğ“ 0) :=
      tendsto_zero_of_tendsto_atTop_of_tendsto_smul (v := c) id (by aesop) m_to_infty a_in_s hma
    rw [hasDerivAt_iff_hasFDerivAt, hasFDerivAt_iff_isLittleO_nhds_zero] at hDf
    simp only [zero_add, hf, sub_zero, ContinuousLinearMap.smulRight_apply,
               ContinuousLinearMap.one_apply] at hDf
    apply IsLittleO.comp_tendsto hDf a_lim
  have ma_isBigO : (fun i â†¦ m i * a i) =O[L] fun _ â†¦ (1 : â„) := by
    apply mul_isBigO_one_of_tendsto_atTop_of_tendsto_smul_apply (v := c) id rfl (by aesop)
            m_to_infty a_in_s ?_ one_ne_zero (hasDerivAt_id' 0)
    simpa [smul_eq_mul] using hma
  have key : (fun i â†¦ (m i) â€¢ f (a i) - (m i * a i) â€¢ D) =o[L] (fun i â†¦ (1 : â„)) := by
    have key' := (isBigO_refl m L).smul_isLittleO hDfa
    simp only [smul_sub, smul_eq_mul] at key'
    simpa [mul_smul] using key'.trans_isBigO ma_isBigO
  suffices key_diff : (fun i â†¦ (m i) â€¢ f (a i) - (m i * a i) â€¢ D) =o[L] (fun i â†¦ (1 : â„)) by
    apply NormedSpace.tendsto_of_tendsto_of_sub_isLittleO_one (Tendsto.smul_const hma D)
    exact isLittleO_comm.mp key
  rw [hasDerivAt_iff_hasFDerivAt, hasFDerivAt_iff_isLittleO_nhds_zero] at hDf
  simp only [zero_add, hf, sub_zero, ContinuousLinearMap.smulRight_apply,
             ContinuousLinearMap.one_apply] at hDf
  have newkey := (isBigO_refl m L).smul_isLittleO hDfa
  simp only [smul_sub, smul_eq_mul] at newkey
  simpa [mul_smul] using newkey.trans_isBigO ma_isBigO

lemma tendsto_smul_apply_smul_deriv_of_tendsto_atTop_of_tendsto_smul_apply_smul_deriv
    {Î¹ : Type*} {L : Filter Î¹} [NeBot L] {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    (fâ‚ fâ‚‚ : â„ â†’ E) (hfâ‚ : fâ‚ 0 = 0) (hfâ‚‚ : fâ‚‚ 0 = 0) {s : Set â„}
    (hsâ‚ : âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ x âˆˆ s, â€–fâ‚ xâ€– < Îµ â†’ x âˆˆ Metric.ball 0 Î´)
    {m : Î¹ â†’ â„} (m_to_infty : L.Tendsto m atTop)
    {a : Î¹ â†’ â„} (a_in_s : L.Eventually fun i â†¦ a i âˆˆ s) {c : â„}
    {Dâ‚ Dâ‚‚ : E} (Dâ‚_ne_zero : Dâ‚ â‰  0) (hDfâ‚ : HasDerivAt fâ‚ Dâ‚ 0) (hDfâ‚‚ : HasDerivAt fâ‚‚ Dâ‚‚ 0)
    (ha : L.Tendsto (fun i â†¦ m i â€¢ (fâ‚ (a i))) (ğ“ (c â€¢ Dâ‚))) :
    L.Tendsto (fun i â†¦ m i â€¢ (fâ‚‚ (a i))) (ğ“ (c â€¢ Dâ‚‚)) := by
  have obs := tendsto_mul_smul_deriv_of_tendsto_atTop_of_tendsto_smul_apply fâ‚ hfâ‚ hsâ‚
                m_to_infty a_in_s ha Dâ‚_ne_zero hDfâ‚
  have aux := (tendsto_smul_self_iff_tendsto_of_ne_zero Dâ‚_ne_zero).mp obs
  simpa using tendsto_smul_apply_smul_deriv_of_tendsto_atTop_of_tendsto_mul fâ‚‚ hfâ‚‚
                m_to_infty a_in_s aux hDfâ‚‚

end limit_relation_manipulation_lemmas


section actual_limit_relation_manipulation

lemma ev_limit_iff_log_ev_limit {F G : CumulativeDistributionFunction}
    {As : â„• â†’ orientationPreservingAffineEquiv} {x : â„} (hGx : G x âˆˆ Ioo 0 1) :
    (Tendsto (fun n â†¦ ((As n â€¢ F) x)^n) atTop (ğ“ (G x)))
      â†” (Tendsto (fun n â†¦ n * Real.log (((As n) â€¢ F) x)) atTop (ğ“ (Real.log (G x)))) := by
  sorry -- **Issue #26**

lemma tendsto_one_of_ev_limit {F G : CumulativeDistributionFunction}
    {As : â„• â†’ orientationPreservingAffineEquiv} {x : â„} (hGx : G x âˆˆ Ioo 0 1)
    (h : Tendsto (fun n â†¦ ((As n â€¢ F) x)^n) atTop (ğ“ (G x))) :
    Tendsto (fun n â†¦ ((As n â€¢ F) x)) atTop (ğ“ 1) := by
  sorry -- **Issue #28**

lemma log_ev_limit_iff_taylored_ev_limit {F G : CumulativeDistributionFunction}
    {As : â„• â†’ orientationPreservingAffineEquiv} {x : â„} (hGx : G x âˆˆ Ioo 0 1) :
    (Tendsto (fun n â†¦ n * Real.log (((As n) â€¢ F) x)) atTop (ğ“ (Real.log (G x))))
      â†” (Tendsto (fun n â†¦ n * (1 - (((As n) â€¢ F) x))) atTop (ğ“ (-(Real.log (G x))))) := by
  sorry -- **Issue #27**

lemma taylored_ev_limit_iff_oneDivOneSub_limit {F G : CumulativeDistributionFunction}
    {As : â„• â†’ orientationPreservingAffineEquiv} {x : â„} (hGx : G x âˆˆ Ioo 0 1) :
    (Tendsto (fun n â†¦ n * (1 - (((As n) â€¢ F) x))) atTop (ğ“ (-(Real.log (G x)))))
      â†” (Tendsto (fun n â†¦ 1/(n * (1 - (((As n) â€¢ F) x)))) atTop (ğ“ (1/(-(Real.log (G x)))))) := by
  simp only [one_div]
  have log_Gx_ne_zero : Real.log (G x) â‰  0 := Real.log_ne_zero_of_pos_of_ne_one hGx.1 hGx.2.ne
  have nlog_Gx_ne_zero := neg_ne_zero.mpr log_Gx_ne_zero
  constructor
  Â· intro h_lim
    exact Tendsto.invâ‚€ h_lim nlog_Gx_ne_zero
  Â· intro h_invlim
    simpa only [inv_inv] using Tendsto.invâ‚€ h_invlim (inv_ne_zero nlog_Gx_ne_zero)

open ENNReal

theorem tfae_ev_limit_relation {F G : CumulativeDistributionFunction}
    (As : â„• â†’ orientationPreservingAffineEquiv) {x : â„} (hGx : G x âˆˆ Ioo 0 1) :
    List.TFAE
      [Tendsto (fun n â†¦ ((As n â€¢ F) x)^n) atTop (ğ“ (G x)),
       Tendsto (fun n â†¦ n * Real.log (((As n) â€¢ F) x)) atTop (ğ“ (Real.log (G x))),
       Tendsto (fun n â†¦ n * (1 - (((As n) â€¢ F) x))) atTop (ğ“ (-(Real.log (G x)))),
       Tendsto (fun n â†¦ 1/(n * (1 - (((As n) â€¢ F) x)))) atTop (ğ“ (1/(-(Real.log (G x))))),
       Tendsto (fun (n : â„•) â†¦ (n : â„â‰¥0âˆ)â»Â¹ * (((As n) â€¢ F).oneDivOneSub x))
          atTop (ğ“ (G.oneDivNegLog x))] := by
  have one_iff_two := ev_limit_iff_log_ev_limit hGx (As := As) (F := F) (G := G)
  have two_iff_three := log_ev_limit_iff_taylored_ev_limit hGx (As := As) (F := F) (G := G)
  have three_iff_four := taylored_ev_limit_iff_oneDivOneSub_limit hGx (As := As) (F := F) (G := G)
  have four_iff_five := oneDivSub_limit_iff hGx (As := As) (F := F) (G := G)
  tfae_finish

end actual_limit_relation_manipulation
