/-
Copyright (c) 2025 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§, ...
-/
import Mathlib.Order.CompletePartialOrder
import Mathlib.Order.OrdContinuous
import Mathlib.Topology.Order.DenselyOrdered
import Mathlib

open Set

noncomputable section order_continuity_vs_continuity
/-!
# Left/right-order-continuous functions are (topologically) left/right-continuous

Below is an extended version of PR #23309 to Mathlib: here we do the pointwise
`ContinuousAt` case with weaker assumptions, and the converse implication as well.
These are auxiliary results for the ExtremeValueProject.
-/

section ConditionallyCompleteLinearOrder

variable {R S : Type*} [LinearOrder R] [LinearOrder S] [DenselyOrdered R]

lemma forall_isLUB_imp_isLUB_image_iff_isLUB_image_Iio {f : R ‚Üí S} (f_mono : Monotone f) {x : R} :
    (‚àÄ (s : Set R), IsLUB s x ‚Üí IsLUB (f '' s) (f x)) ‚Üî IsLUB (f '' Iio x) (f x) := by
  constructor
  ¬∑ intro f_lcont
    exact f_lcont _ isLUB_Iio
  ¬∑ intro h s hx
    refine ‚ü®by simpa [mem_upperBounds] using fun a ha ‚Ü¶ f_mono (hx.1 ha), ?_‚ü©
    intro y hy
    apply h.2
    simp only [mem_lowerBounds, mem_upperBounds, mem_image, forall_exists_index, and_imp,
               forall_apply_eq_imp_iff‚ÇÇ] at hy ‚ä¢
    intro a a_lt
    obtain ‚ü®b, b_in_s, hb‚ü© : ‚àÉ b ‚àà s, a < b := by rwa [mem_Iio, lt_isLUB_iff hx] at a_lt
    by_contra con
    apply lt_irrefl _ <| (not_le.mp con).trans_le <| (f_mono hb.le).trans (hy b b_in_s)

lemma forall_isGLB_imp_isGLB_image_iff_isGLB_image_Ioi {f : R ‚Üí S} (f_mono : Monotone f) {x : R} :
    (‚àÄ (s : Set R), IsGLB s x ‚Üí IsGLB (f '' s) (f x)) ‚Üî IsGLB (f '' Ioi x) (f x) :=
  forall_isLUB_imp_isLUB_image_iff_isLUB_image_Iio (R := R·µí·µà) (S := S·µí·µà) <|
    by exact fun _ _ hx ‚Ü¶ f_mono hx

lemma leftOrdContinuous_iff_forall_isLUB_image_Iio {f : R ‚Üí S} (f_mono : Monotone f) :
    LeftOrdContinuous f ‚Üî ‚àÄ (x : R), IsLUB (f '' Iio x) (f x) := by
  simp_rw [‚Üê forall_isLUB_imp_isLUB_image_iff_isLUB_image_Iio f_mono, LeftOrdContinuous]
  aesop

lemma rightOrdContinuous_iff_forall_isGLB_image_Ioi {f : R ‚Üí S} (f_mono : Monotone f) :
    RightOrdContinuous f ‚Üî ‚àÄ (x : R), IsGLB (f '' Ioi x) (f x) :=
  leftOrdContinuous_iff_forall_isLUB_image_Iio (R := R·µí·µà) (S := S·µí·µà) <|
    by exact fun _ _ hx ‚Ü¶ f_mono hx

open Topology

lemma Monotone.isLUB_image_Iio_of_continuousWithinAt_Iic' {R S : Type*}
    [PartialOrder R] [TopologicalSpace R] [PartialOrder S] [TopologicalSpace S]
    [ClosedIicTopology S] {f : R ‚Üí S} (f_mono : Monotone f) {x : R}
    (hx : (ùìù[<] x).NeBot) (f_cont : ContinuousWithinAt f (Iic x) x) :
    IsLUB (f '' Iio x) (f x) := by
  rw [‚Üê continuousWithinAt_Iio_iff_Iic] at f_cont
  refine ‚ü®?_, ?_‚ü©
  ¬∑ simpa [mem_upperBounds] using fun a ha ‚Ü¶ f_mono ha.le
  ¬∑ simp only [mem_lowerBounds, mem_upperBounds, mem_image, mem_Iio, forall_exists_index,
               and_imp, forall_apply_eq_imp_iff‚ÇÇ]
    intro y hy
    apply le_of_tendsto_of_frequently f_cont
    apply Filter.frequently_iff.mpr
    intro U hU
    obtain ‚ü®t, ht‚ü© := Filter.Eventually.exists (Filter.inter_mem hU self_mem_nhdsWithin)
    exact ‚ü®t, ht.1, hy _ ht.2‚ü©

lemma Monotone.isLUB_image_Iio_of_continuousWithinAt_Iic {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [PartialOrder S] [TopologicalSpace S] [ClosedIicTopology S]
    {f : R ‚Üí S} (f_mono : Monotone f) {x : R} (hx : ¬¨ IsMin x)
    (f_cont : ContinuousWithinAt f (Iic x) x) :
    IsLUB (f '' Iio x) (f x) := by
  apply f_mono.isLUB_image_Iio_of_continuousWithinAt_Iic' _ f_cont
  apply mem_closure_iff_nhdsWithin_neBot.mp
  rw [closure_Iio' (Iio_nonempty.mpr hx)]
  exact right_mem_Iic

lemma Monotone.continuousWithinAt_Iic_of_isLUB_image_Iio {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R]
    [LinearOrder S] [TopologicalSpace S] [OrderTopology S]
    {f : R ‚Üí S} (f_mono : Monotone f) {x : R} (hf : IsLUB (f '' Iio x) (f x)) :
    ContinuousWithinAt f (Iic x) x := by
  rw [ContinuousWithinAt, OrderTopology.topology_eq_generate_intervals (Œ± := S)]
  simp_rw [TopologicalSpace.tendsto_nhds_generateFrom_iff, mem_nhdsWithin]
  rintro V ‚ü®z, rfl | rfl‚ü© hxz
  -- The case `V = Ioi z`.
  ¬∑ obtain ‚ü®_, ‚ü®a, hax, rfl‚ü©, hza‚ü© := (lt_isLUB_iff <| hf).mp hxz
    exact ‚ü®Ioi a, isOpen_Ioi, hax, fun b hab ‚Ü¶ hza.trans_le <| f_mono hab.1.le‚ü©
  -- The case `V = Iio z`.
  ¬∑ exact ‚ü®univ, isOpen_univ, trivial, fun a ha ‚Ü¶ (f_mono ha.2).trans_lt hxz‚ü©

lemma Monotone.continuousWithinAt_Iic_iff_isLUB_image_Iio' {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R]
    [LinearOrder S] [TopologicalSpace S] [OrderTopology S]
    {f : R ‚Üí S} (f_mono : Monotone f) {x : R} (hx : (ùìù[<] x).NeBot) :
    ContinuousWithinAt f (Iic x) x ‚Üî IsLUB (f '' Iio x) (f x) :=
  ‚ü®isLUB_image_Iio_of_continuousWithinAt_Iic' f_mono hx,
   f_mono.continuousWithinAt_Iic_of_isLUB_image_Iio‚ü©

lemma Monotone.continuousWithinAt_Ici_iff_isGLB_image_Ioi' {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R]
    [LinearOrder S] [TopologicalSpace S] [OrderTopology S]
    {f : R ‚Üí S} (f_mono : Monotone f) {x : R} (hx : (ùìù[>] x).NeBot) :
    ContinuousWithinAt f (Ici x) x ‚Üî IsGLB (f '' Ioi x) (f x) :=
  continuousWithinAt_Iic_iff_isLUB_image_Iio' (R := R·µí·µà) (S := S·µí·µà)
    (fun _ _ hx ‚Ü¶ by exact f_mono hx) hx

lemma Monotone.continuousWithinAt_Iic_iff_isLUB_image_Iio {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [LinearOrder S] [TopologicalSpace S] [OrderTopology S]
    {f : R ‚Üí S} (f_mono : Monotone f) {x : R} (hx : ¬¨ IsMin x) :
    ContinuousWithinAt f (Iic x) x ‚Üî IsLUB (f '' Iio x) (f x) :=
  ‚ü®isLUB_image_Iio_of_continuousWithinAt_Iic f_mono hx,
   f_mono.continuousWithinAt_Iic_of_isLUB_image_Iio‚ü©

lemma Monotone.continuousWithinAt_Ici_iff_isGLB_image_Ioi {R S : Type*}
    [LinearOrder R] [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [LinearOrder S] [TopologicalSpace S] [OrderTopology S]
    {f : R ‚Üí S} (f_mono : Monotone f) {x : R} (hx : ¬¨ IsMax x) :
    ContinuousWithinAt f (Ici x) x ‚Üî IsGLB (f '' Ioi x) (f x) :=
  continuousWithinAt_Iic_iff_isLUB_image_Iio (R := R·µí·µà) (S := S·µí·µà)
    (fun _ _ hx ‚Ü¶ by exact f_mono hx) hx

variable {X : Type*} [ConditionallyCompleteLinearOrder X] [TopologicalSpace X] [OrderTopology X]
variable {Y : Type*} [ConditionallyCompleteLinearOrder Y] [TopologicalSpace Y] [OrderTopology Y]
variable [DenselyOrdered X] {f : X ‚Üí Y} {x : X}

/-- An order-theoretically left-continuous function is topologically left-continuous, assuming
the function is between conditionally complete linear orders with order topologies, and the domain
is densely ordered. -/
lemma LeftOrdContinuous.continuousWithinAt_Iic (hf : LeftOrdContinuous f) :
    ContinuousWithinAt f (Iic x) x :=
  Monotone.continuousWithinAt_Iic_of_isLUB_image_Iio hf.mono (hf isLUB_Iio)

/-- An order-theoretically right-continuous function is topologically right-continuous, assuming
the function is between conditionally complete linear orders with order topologies, and the domain
is densely ordered. -/
lemma RightOrdContinuous.continuousWithinAt_Ici (hf : RightOrdContinuous f) :
    ContinuousWithinAt f (Ici x) x := hf.orderDual.continuousWithinAt_Iic

/-- A topologically left-continuous function is order-theoretically left-continuous, assuming
the function is between conditionally complete linear orders with order topologies, and the domain
is densely ordered and has no minimum. -/
lemma Monotone.leftOrdContinuous_of_forall_continuousWithinAt_Iic [NoMinOrder X]
    (f_mono : Monotone f) (hf : ‚àÄ x, ContinuousWithinAt f (Iic x) x) :
    LeftOrdContinuous f := by
  refine (leftOrdContinuous_iff_forall_isLUB_image_Iio f_mono).mpr ?_
  intro x
  have key := @continuousWithinAt_Iic_iff_isLUB_image_Iio' X Y _ _ _ _ _ _ f f_mono x (nhdsLT_neBot x)
  simpa [‚Üê key] using hf x

/-- A topologically right-continuous function is order-theoretically right-continuous, assuming
the function is between conditionally complete linear orders with order topologies, and the domain
is densely ordered and has no maximum. -/
lemma Monotone.rightOrdContinuous_of_forall_continuousWithinAt_Ici [NoMaxOrder X]
    (f_mono : Monotone f) (hf : ‚àÄ x, ContinuousWithinAt f (Ici x) x) :
    RightOrdContinuous f :=
  leftOrdContinuous_of_forall_continuousWithinAt_Iic (X := X·µí·µà) (Y := Y·µí·µà)
    (fun _ _ hx ‚Ü¶ by exact f_mono hx) hf

end ConditionallyCompleteLinearOrder

end order_continuity_vs_continuity


noncomputable section modification
/-!
# Left-continuous and right-continuous modifications of functions
-/

variable {R S : Type*}

/-- Left-continuous modification of F. -/
def lcMod [Preorder R] [SupSet S] (F : R ‚Üí S) (x : R) := sSup (F '' Iio x)

/-- right-continuous modification of F. -/
def rcMod [Preorder R] [InfSet S] (F : R ‚Üí S) (x : R) := sInf (F '' Ioi x)

lemma lcMod_mono [Preorder R] [CompleteSemilatticeSup S] (F : R ‚Üí S) :
    Monotone (lcMod F) :=
  fun _ _ hx ‚Ü¶ sSup_le_sSup <| image_mono <| Iio_subset_Iio hx

lemma rcMod_mono [Preorder R] [CompleteSemilatticeInf S] (F : R ‚Üí S) :
    Monotone (rcMod F) :=
  fun _ _ hx ‚Ü¶ sInf_le_sInf <| image_mono <| Ioi_subset_Ioi hx

lemma lcMod_apply_le_self_apply [Preorder R] [CompleteSemilatticeSup S] {F : R ‚Üí S}
    (F_mono : Monotone F) (x : R) :
    lcMod F x ‚â§ F x := by
  apply sSup_le
  intro y ‚ü®x', x'_lt, Fx'_eq_y‚ü©
  simpa [‚Üê Fx'_eq_y] using F_mono x'_lt.le

lemma self_apply_le_rcMod_apply [Preorder R] [CompleteSemilatticeInf S] {F : R ‚Üí S}
    (F_mono : Monotone F) (x : R) :
    F x ‚â§ rcMod F x :=
  lcMod_apply_le_self_apply (R := R·µí·µà) (S := S·µí·µà) (F := F) (by exact fun _ _ hx ‚Ü¶ F_mono hx) x

lemma lcMod_le_self [Preorder R] [CompleteSemilatticeSup S] {F : R ‚Üí S} (F_mono : Monotone F) :
    lcMod F ‚â§ F :=
  fun x ‚Ü¶ lcMod_apply_le_self_apply F_mono x

lemma self_le_rcMod [Preorder R] [CompleteSemilatticeInf S] {F : R ‚Üí S} (F_mono : Monotone F) :
    F ‚â§ rcMod F :=
  fun x ‚Ü¶ self_apply_le_rcMod_apply F_mono x

lemma le_lcMod_apply_of_lt [Preorder R] [CompleteSemilatticeSup S] (F : R ‚Üí S) {x' x : R}
    (h : x' < x) :
    F x' ‚â§ lcMod F x :=
  le_sSup (mem_image_of_mem F h)

lemma rcMod_apply_le_of_lt [Preorder R] [CompleteSemilatticeInf S] (F : R ‚Üí S) {x' x : R}
    (h : x < x') :
    rcMod F x ‚â§ F x' :=
  sInf_le (mem_image_of_mem F h)

/-- The left-continuous modification of a function is left-continuous. -/
lemma leftOrdContinuous_lcMod [LinearOrder R] [CompleteSemilatticeSup S] (F : R ‚Üí S) :
    LeftOrdContinuous (lcMod F) := by
  intro s lub_s h_lub_s
  refine ‚ü®?_, ?_‚ü©
  ¬∑ simp only [mem_upperBounds, mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ]
    intro x x_in_s
    exact lcMod_mono _ (h_lub_s.1 x_in_s)
  ¬∑ simp only [mem_lowerBounds, mem_upperBounds, mem_image, forall_exists_index, and_imp,
               forall_apply_eq_imp_iff‚ÇÇ]
    intro y hy
    apply sSup_le
    intro z ‚ü®x, hx, Fx_eq_z‚ü©
    rw [‚Üê Fx_eq_z]
    obtain ‚ü®x', x'_in_s, x_lt_x'‚ü© : ‚àÉ x' ‚àà s, x < x' := by
      by_contra con
      simp only [not_exists, not_and, not_lt] at con
      exact lt_irrefl _ (hx.trans_le (h_lub_s.2 con))
    exact (le_lcMod_apply_of_lt F x_lt_x').trans (hy x' x'_in_s)

/-- The right-continuous modification of a function is right-continuous. -/
lemma rightOrdContinuous_rcMod [LinearOrder R] [CompleteSemilatticeInf S](F : R ‚Üí S) :
    RightOrdContinuous (rcMod F) :=
  leftOrdContinuous_lcMod (R := R·µí·µà) (S := S·µí·µà) (F := F)

section OrderTopology

open Topology Filter

variable {R S : Type*}

lemma lcMod_apply_eq_self_apply' [CompleteLattice R] [TopologicalSpace R]
    [CompleteLattice S] [TopologicalSpace S] [ClosedIicTopology S]
    {F : R ‚Üí S} (F_mono : Monotone F) {x : R}
    (hFx : ContinuousAt F x) (lt_x_neBot : (ùìù[<] x).NeBot) :
    lcMod F x = F x := by
  apply le_antisymm (lcMod_apply_le_self_apply F_mono x)
  have aux : Filter.Tendsto F (ùìù[<] x) (ùìù (F x)) := tendsto_nhdsWithin_of_tendsto_nhds hFx
  apply le_of_tendsto_of_frequently aux (b := lcMod F x)
  apply Eventually.frequently
  filter_upwards [self_mem_nhdsWithin] with z hz using le_lcMod_apply_of_lt F hz

lemma lcMod_apply_eq_self_apply
    [CompleteLinearOrder R] [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [CompleteLattice S] [TopologicalSpace S] [ClosedIicTopology S] {F : R ‚Üí S}
    (F_mono : Monotone F) {x : R} (hFx : ContinuousAt F x) (x_ne_bot : ¬¨ IsMin x) :
    lcMod F x = F x := by
  apply lcMod_apply_eq_self_apply' F_mono hFx
  exact nhdsWithin_Iio_self_neBot' (Iio_nonempty.mpr x_ne_bot)

lemma rcMod_apply_eq_self_apply' [CompleteLattice R] [TopologicalSpace R]
    [CompleteLattice S] [TopologicalSpace S] [ClosedIciTopology S]
    {F : R ‚Üí S} (F_mono : Monotone F) (x : R)
    (hFx : ContinuousAt F x) (gt_x_neBot : (ùìù[>] x).NeBot) :
    rcMod F x = F x :=
  lcMod_apply_eq_self_apply' (R := R·µí·µà) (S := S·µí·µà) (fun _ _ hx ‚Ü¶ by exact F_mono hx) hFx gt_x_neBot

lemma rcMod_apply_eq_self_apply
    [CompleteLinearOrder R] [TopologicalSpace R] [OrderTopology R] [DenselyOrdered R]
    [CompleteLattice S] [TopologicalSpace S] [ClosedIciTopology S] {F : R ‚Üí S}
    (F_mono : Monotone F) (x : R) (hFx : ContinuousAt F x) (x_ne_top : ¬¨ IsMax x) :
    rcMod F x = F x :=
  lcMod_apply_eq_self_apply (R := R·µí·µà) (S := S·µí·µà) (fun _ _ hx ‚Ü¶ by exact F_mono hx) hFx x_ne_top

variable [CompleteLattice R] [CompleteLattice S]

end OrderTopology

end modification
